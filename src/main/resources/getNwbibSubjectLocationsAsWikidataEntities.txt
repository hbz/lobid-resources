https://query.wikidata.org/sparql?query=
# Get administrative areas in North Rhine-Westphalia with German labels
SELECT DISTINCT 
?item 
?itemLabel 
?partOf
?gnd
?dissolutionDate
(group_concat(distinct ?hasPart; separator = ", ") as ?parts)
WHERE {
  hint:Query hint:optimizer "None".
  { ?item wdt:P131* wd:Q1198 .
       { ?item wdt:P31 wd:Q22721 . } # Regierungsbezirk
   UNION
       { ?item wdt:P31 wd:Q106658 . } # Landkreis
   UNION
       { ?item wdt:P31 wd:Q262166 . } # Gemeinde
   UNION
       { ?item wdt:P31 wd:Q22865 . } # kreisfreie Stadt
   UNION
       { ?item wdt:P31 wd:Q253019 . } # Ortsteil
  }
  SERVICE wikibase:label {  bd:serviceParam wikibase:language "de" }
  OPTIONAL { ?item wdt:P227 ?gnd }
  OPTIONAL { ?item wdt:P576 ?dissolutionDate }
  OPTIONAL { ?item wdt:P150 ?hasPart . }
  OPTIONAL { ?item wdt:P131 ?partOf }
}

GROUP BY ?item ?itemLabel ?partOf ?gnd ?dissolutionDate
