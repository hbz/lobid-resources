
copy_field("almaMmsId", "describedBy.id")
prepend("describedBy.id", "http://lobid.org/resources/")

copy_field("almaMmsId", "describedBy.label")
prepend("describedBy.label", "Webseite der hbz-Ressource ")

set_array("describedBy.type[]", "BibliographicDescription")

# MNG is a ALMA-specific element

copy_field("MNG  .b","describedBy.dateCreated")
copy_field("MNG  .d","describedBy.dateModified")
replace_all("describedBy.dateCreated","-","")
replace_all("describedBy.dateCreated"," .*","")
replace_all("describedBy.dateCreated","c|©|\\s?|,|.|:|;|/|=","")
replace_all("describedBy.dateModified","-","")
replace_all("describedBy.dateModified"," .*","")
replace_all("describedBy.dateModified","c|©|\\s?|,|.|:|;|/|=","")
unless any_match("describedBy.dateCreated","\\d{8}|\\d{4}")
	remove_field("describedBy.dateCreated")
end
unless any_match("describedBy.dateModified","\\d{8}|\\d{4}")
	remove_field("describedBy.dateModified")
end

add_field("describedBy.inDataset.id","http://lobid.org/resources/dataset#!")

add_field("describedBy.inDataset.label","lobid-resources – Der hbz-Verbundkatalog als Linked Open Data")

set_array("describedBy.resultOf.type[]", "CreateAction")

add_field("describedBy.resultOf.endTime","0000-00-00T00:00:00")
# TODO: Add timestamp at the moment not supported by FIX.


add_field("describedBy.resultOf.instrument.id","https://github.com/hbz/lobid-resources")

set_array("describedBy.resultOf.instrument.type[]", "SoftwareApplication")

add_field("describedBy.resultOf.instrument.label","Software lobid-resources")

copy_field("almaMmsId","describedBy.resultOf.object.id")
prepend("describedBy.resultOf.object.id","https://lobid.org/hbz01/")


set_array("describedBy.resultOf.object.type[]", "DataFeedItem")

copy_field("almaMmsId","describedBy.resultOf.object.label")
prepend("describedBy.resultOf.object.label","hbz-Ressource ")
append("describedBy.resultOf.object.label"," im Exportformat MARC21 XML")

add_field("describedBy.resultOf.object.inDataset.id", "https://datahub.io/dataset/hbz_unioncatalog")

add_field("describedBy.resultOf.object.inDataset.label", "hbz_unioncatalog")

set_array("describedBy.license[]")
add_field("describedBy.license[].$append.id","http://creativecommons.org/publicdomain/zero/1.0" )
add_field("describedBy.license[].$last.label","Creative Commons-Lizenz CC0 1.0 Universal" )


# TODO: It seems that there are a lot of organisations that are not in lobid, we should filter them out.

# 040 - Cataloging Source (NR) - Subfield: $a (NR), $c (NR), $d (R)
# ALMA has a lot of invalid repeated subfields $a

do list(path: "040  ", "var":"$i")

  do list(path:"$i.a","var":"$j")
    do once("040  .a")
      copy_field("$j", "describedBy.sourceOrganization.id")
    end
  end
  do list(path:"$i.c","var":"$j")
    do once("040  .c")
      copy_field("$j", "describedBy.provider.id")
    end
  end

# TODO: 040  .d is repeatable but lobid modifiedBy is only a simple object. How to deal with this?
  do list(path:"$i.d", "var":"$j")
      unless any_match("$j", "9999|0001")
        do once("040  .d")
          copy_field("$j", "describedBy.modifiedBy.id")
        end
      end
  end

end

replace_all("describedBy.sourceOrganization.id", " ", "")
prepend("describedBy.sourceOrganization.id", "http://lobid.org/organisations/DE-")
append("describedBy.sourceOrganization.id", "#!")
if exists("describedBy.sourceOrganization.id")
	add_field("describedBy.sourceOrganization.label","lobid Organisation")
end

replace_all("describedBy.provider.id", " ", "")
prepend("describedBy.provider.id", "http://lobid.org/organisations/DE-")
append("describedBy.provider.id", "#!")
if exists("describedBy.provider.id")
	add_field("describedBy.provider.label","lobid Organisation")
end

replace_all("describedBy.modifiedBy.id", "(DE-)?(.*)", "$2")
replace_all("describedBy.modifiedBy.id", " ", "")
prepend("describedBy.modifiedBy.id", "http://lobid.org/organisations/DE-")
append("describedBy.modifiedBy.id", "#!")
if exists("describedBy.modifiedBy.id")
	add_field("describedBy.modifiedBy.label","lobid Organisation")
end
