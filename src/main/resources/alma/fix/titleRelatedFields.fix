# 245 - Title Statement (NR) - Subfield: $a (NR), $b (NR), $n (R), $p (R)

copy_field("245??.a","@title")
unless any_match("@title",".*\\.\\.\\.")
  replace_all("@title", "^[©]|\\s?[,.:;/=]?$", "")
end
replace_all("@title","<<|>>","")


add_array("@titleOfSubSeries_n") # Subvolume numbering
do list(path:"245??.n", "var":"$i")
  unless all_equal("$i","[...]")
    copy_field("$i","@titleOfSubSeries_n.$append")
  end
end
join_field("@titleOfSubSeries_n", ". ")

add_array("@titleOfSubSeries_p")

do list(path:"245??.p", "var":"$i") # Subvolume title
  unless all_equal("$i","[...]")
    copy_field("$i","@titleOfSubSeries_p.$append")
  end
end
replace_all("@titleOfSubSeries_p.*", "\\s?\\.?\\s?$", "")
replace_all("@titleOfSubSeries_p.*","<<|>>","")
join_field("@titleOfSubSeries_p", ". ")
if is_empty("@titleOfSubSeries_p")
  remove_field("@titleOfSubSeries_p")
end
if is_empty("@titleOfSubSeries_n")
  remove_field("@titleOfSubSeries_n")
end
if exists("@titleOfSubSeries_n") # separate subvolume numbering and title with :
  prepend("@titleOfSubSeries_p", ": ")
end

paste("@titleOfSubSeries", "@titleOfSubSeries_n",  "@titleOfSubSeries_p", join_char: "")

if is_empty("@titleOfSubSeries")
  remove_field("@titleOfSubSeries")
end

prepend("@titleOfSubSeries",", ")

paste("title","@title", "@titleOfSubSeries", join_char: "") # title consists of main title + if exists: subvolume numbering and subvolume title


# alternateGraphicRepresentation for title, otherTitleInformation, alternativetitle, edition and note

do list(path:"245??","var":"$i")
  if exists("$i.6")
    copy_field("$i.6","$i.linkageTest")
    do list(path:"880??","var":"$880")
      if in ("$i.linkageTest","$880.linkageTest")
        if in ("$880.@script.id","alternateGraphicRepresentation[].*.script.id")
          do list(path:"alternateGraphicRepresentation[]","var":"$AGR")
            if in ("$880.@script.id","$AGR.script.id")
            copy_field("$880.a","$AGR.record.@title")
            copy_field("$880.n","$AGR.record.@titleOfSubSeries_n")
            unless exists("$AGR.record.@titleOfSubSeries_p")
              add_array("$AGR.record.@titleOfSubSeries_p")
            end
            copy_field("$880.p","$AGR.record.@titleOfSubSeries_p.$append")
            unless exists("$AGR.record.otherTitleInformation[]")
              add_array("$AGR.record.otherTitleInformation[]")
            end
            copy_field("$880.b","$AGR.record.otherTitleInformation[].$append")
            unless exists("$AGR.record.responsibilityStatement[]")
              add_array("$AGR.record.responsibilityStatement[]")
            end
            copy_field("$880.c","$AGR.record.responsibilityStatement[].$append")
            end
          end
        else
        copy_field("$880.@script.id","alternateGraphicRepresentation[].$append.script.id")
        copy_field("$880.@script.label","alternateGraphicRepresentation[].$last.script.label")
        copy_field("$880.a","alternateGraphicRepresentation[].$last.record.@title")
        copy_field("$880.n","alternateGraphicRepresentation[].$last.record.@titleOfSubSeries_n")
        add_array("alternateGraphicRepresentation[].$last.record.@titleOfSubSeries_p")
        copy_field("$880.p","alternateGraphicRepresentation[].$last.record.@titleOfSubSeries_p.$append")
        add_array("alternateGraphicRepresentation[].$last.record.otherTitleInformation[]")
        copy_field("$880.b","alternateGraphicRepresentation[].$last.record.otherTitleInformation[].$append")
        add_array("alternateGraphicRepresentation[].$last.record.responsibilityStatement[]")
        copy_field("$880.c","alternateGraphicRepresentation[].$last.record.responsibilityStatement[].$append")
        end
      end
    end
  end
end

do list(path:"alternateGraphicRepresentation[]","var":"$AGR")
  if exists("$AGR.record.@title")
    unless exists("$AGR.record.@titleOfSubSeries_n")
      copy_field("@titleOfSubSeries_n","$AGR.record.@titleOfSubSeries_n")
    end
    join_field("$AGR.record.@titleOfSubSeries_p", ". ")
    if is_empty("$AGR.record.@titleOfSubSeries_p")
      remove_field("$AGR.record.@titleOfSubSeries_p")
    end
    if exists("$AGR.record.@titleOfSubSeries_n") # separate subvolume numbering and title with :
      prepend("$AGR.record.@titleOfSubSeries_p", ": ")
    end
    if is_empty("$AGR.record.@titleOfSubSeries_n")
      remove_field("$AGR.record.@titleOfSubSeries_n")
    end
    paste("$AGR.record.@titleOfSubSeries", "$AGR.record.@titleOfSubSeries_n",  "$AGR.record.@titleOfSubSeries_p", join_char: "")

    if is_empty("$AGR.record.@titleOfSubSeries")
      remove_field("$AGR.record.@titleOfSubSeries")
    end

    prepend("$AGR.record.@titleOfSubSeries",", ")

    paste("$AGR.record.title","$AGR.record.@title", "$AGR.record.@titleOfSubSeries", join_char: "") # title consists of main title + if exists: subvolume numbering and subvolume title
    remove_field("$AGR.record.@title")
    remove_field("$AGR.record.@titleOfSubSeries")
    remove_field("$AGR.record.@titleOfSubSeries_n")
    remove_field("$AGR.record.@titleOfSubSeries_p")
  elsif exists("$AGR.record.@titleOfSubSeries_p") # We have an appearance where there is no title $a is given as alternateGraphicRepresentation
    join_field("$AGR.record.@titleOfSubSeries_p", ". ")
    if is_empty("$AGR.record.@titleOfSubSeries_p")
      remove_field("$AGR.record.@titleOfSubSeries_p")
    end
    if exists("$AGR.record.@titleOfSubSeries_n") # separate subvolume numbering and title with :
      prepend("$AGR.record.@titleOfSubSeries_p", ": ")
    end
    unless exists("$AGR.record.@titleOfSubSeries_n")
      copy_field("@titleOfSubSeries_n","$AGR.record.@titleOfSubSeries_n")
    end
    if exists("$AGR.record.@titleOfSubSeries_n") # separate subvolume numbering and title with :
      prepend("$AGR.record.@titleOfSubSeries_p", ": ")
    end

    paste("$AGR.record.title", "$AGR.record.@titleOfSubSeries_n",  "$AGR.record.@titleOfSubSeries_p", join_char: "")
    remove_field("$AGR.record.@titleOfSubSeries_n")
    remove_field("$AGR.record.@titleOfSubSeries_p")
  end
end

# 246 - Varying Form of Title (R) - Subfields: $a (NR)

do list(path:"246?[ 345678]","var":"$i")
  prepend("$i.b",". ")
  paste("alternativeTitle[].$append", "$i.a",  "$i.b", join_char: "")
  call_macro("alternateGraphicRepresationArrayOfStrings",targetArray:"alternativeTitle[]",variable:"$i")
end


# 247 - Former Title (R)
do list(path: "247??", "var":"$i")
  if exists("$i.f")
    paste("alternativeTitle[].$append","$i.a", "~(","$i.f","~)")
  else
    copy_field("$i.a","alternativeTitle[].$append")
  end
end

replace_all("alternativeTitle[].*","<<|>>","")

add_array("otherTitleInformation[]")
copy_field("245??.b","otherTitleInformation[].$append")


# 249 beigefügtes Werk Subfield $a
do list(path:"249  ","var":"$i")
  copy_field("$i.a","otherTitleInformation[].$append")
  call_macro("alternateGraphicRepresationArrayOfStrings",targetArray:"otherTitleInformation[]",variable:"$i")
end
unless any_match("otherTitleInformation[].*",".*\\.\\.\\.")
  replace_all("otherTitleInformation[].*","\\s?[./]\\s?$","")
end

# 250 - Edition Statement (R) - Subfields: $a (NR)

add_array("edition[]")
copy_field("250  .a","edition[].$append")
replace_all("edition[].*","\\s?[./]\\s?$","")

do list(path:"250  ","var":"$i")
  call_macro("alternateGraphicRepresationArrayOfStrings",targetArray:"edition[]",variable:"$i")

end

# 260 - Publication, Distribution, etc. (Imprint) (R) - Subfield: $a (R), $b (R), $c (R)
# 264 - Production, Publication, Distribution, Manufacture, and Copyright Notice (R) -  Subfield: $a (R), $b (R), $c  (R)
# 008,18 for frequency
# 515 - Numbering Peculiarities Note (R)
# Prefer 264 over 260 since it can create duplicate info and 260 is discontinued in RDA.

add_array("publication[]")
if exists("264[ 23][ 1]")
  call_macro("publication",field:"2643[ 1]") # 3 - Current/Latest
  call_macro("publication",field:"2642[ 1]") # 2 - Intervening
  call_macro("publication",field:"264 [ 1]") # # - Not applicable/No information provided/Earliest
else
  call_macro("publication",field:"2603[ 1]") # 3 - Current/Latest
  call_macro("publication",field:"2602[ 1]") # 2 - Intervening
  call_macro("publication",field:"260 [ 1]") # # - Not applicable/No information provided/Earliest
end

add_array("manufacture[]")
if exists("264[ 23]3")
  call_macro("manufacture",field:"26433") # 3 - Current/Latest
  call_macro("manufacture",field:"26423") # 2 - Intervening
  call_macro("manufacture",field:"264 3") # # - Not applicable/No information provided/Earliest
end

# Only add additional publication info to the first publication-object since it is the latest.

if exists("publication[].$first")
  copy_field("362??.a","publication[].$first.publicationHistory")
    add_array("publication[].$first.frequency[]")
    if any_match("leader","^.{6}(a[bis]|m[bis]).*$") # checks if continous ressource
      unless any_match("008","^.{18}[#\\| u].*$") # filters out not matching values and also the value unknown
        copy_field("008","publication[].$first.frequency[].$append.id")
        replace_all("publication[].$first.frequency[].$last.id", "^.{18}(.).*$", "http://marc21rdf.info/terms/continuingfre#$1")
        copy_field("publication[].$first.frequency[].$last.id","publication[].$first.frequency[].$last.label")
        lookup("publication[].$first.frequency[].$last.label","marc-publication-frequency-label")
      end
    elsif any_match("006","^s.*$")
      do list(path: "006", "var":"$z")
        if any_match("$z","^s.*$")
          unless any_match("$z","^.[#\\| u].*$")
            copy_field("$z","publication[].$first.frequency[].$append.id")
            replace_all("publication[].$first.frequency[].$last.id", "^.(.).*$", "http://marc21rdf.info/terms/continuingfre#$1")
            copy_field("publication[].$first.frequency[].$last.id","publication[].$first.frequency[].$last.label")
            lookup("publication[].$first.frequency[].$last.label","marc-publication-frequency-label")
          end
        end
      end
    end
    add_array("publication[].$first.note[]")
    copy_field("515??.a","publication[].$first.note[].$append")
  do list(path:"500  ", "var":"$i")
    if any_match("$i.a", "^.*saṃ. \\d{4}=(\\d{4}).*Chr.*")
      remove_field("publication[].$first.startDate")
      copy_field("$i.a","publication[].$first.startDate")
      replace_all("publication[].$first.startDate","^.*saṃ. \\d{4}=(\\d{4}).*Chr.*","$1")
    end
  end
  # Add fallbacks for missing publication dates and other publication info.
  do list(path:"260[ 3][ 1]", "var":"$i")
    do list(path: "$i.c", "var":"$j")
      call_macro("publicationDateCleaner",date:"$j")
      unless exists("publication[].$first.startDate")
        call_macro("publicationStartDate",date:"$j",dateTarget:"publication[].$first.startDate")
      end
      unless exists("publication[].$first.endDate")
        call_macro("publicationEndDate",date:"$j",dateTarget:"publication[].$first.endDate")
      end
      unless exists("publication[].$first.location[].1")
        copy_field("$i.a", "publication[].$first.location[].$append")
      end
      unless exists("publication[].$first.publishedBy[].1")
        copy_field("$i.b", "publication[].$first.publishedBy[].$append")
      end
    end
  end
  unless exists("publication[].$first.startDate")
    if any_match("008","^.{6}[\\|bresticdkmu](\\d{4}).*$")
      copy_field("008","@008startDate")
      replace_all("@008startDate","^.{7}(\\d{4}).*$","$1")
      if any_match("@008startDate",".*?([01]\\d{3}|20\\d{2}).*")
        copy_field("@008startDate","publication[].$first.startDate")
      end
    end
  end
  unless exists("publication[].$first.endDate")
    if any_match("008","^.{6}[cdkmu]\\d{4}(\\d{4}).*$")
      copy_field("008","@008endDate")
      replace_all("@008endDate","^.{11}(\\d{4}).*$","$1")
      unless any_equal("@008endDate","9999")
        copy_field("@008endDate","publication[].$first.endDate")
      end
    end
  end
end




# Mapping for SecondaryPublication
# 533 - Reproduction Note (R)

do list(path:"533  ", "var": "$i")
  add_hash( "publication[].$append")
  add_array("publication[].$last.type[]","SecondaryPublicationEvent")
  add_array("publication[].$last.location[]")
  do list(path:"$i.b","var":"$j")
    copy_field("$j", "publication[].$last.location[].$append")
  end
  add_array("publication[].$last.description[]")
  copy_field("$i.a", "publication[].$last.description[].$append")
  add_array("publication[].$last.publishedBy[]")
  copy_field("$i.c", "publication[].$last.publishedBy[].$append")
  do list(path: "$i.d", "var":"$j")
    call_macro("publicationDateCleaner",date:"$j")
    unless exists("publication[].$last.startDate")
      call_macro("publicationStartDate",date:"$j",dateTarget:"publication[].$last.startDate")
    end
    unless exists("publication[].$last.endDate")
      call_macro("publicationEndDate",date:"$j",dateTarget:"publication[].$last.endDate")
    end
  end
end

# Cleaning up dates of publicaton []
copy_field("leader","@leaderPos07")
substring("@leaderPos07","7","1")
do list(path: "publication[]", "var": "$i")
  replace_all("$i.startDate", ".*?([01]\\d{3}|20\\d{2}).*", "$1")
  if any_match("@leaderPos07","s")
    if any_contain("$i.startDate","0000")
      remove_field("$i.startDate")
    end
  end
  replace_all("$i.endDate", ".*-.*([01]\\d{3}|20\\d{2}).*$", "$1")
  replace_all("$i.location[].*", "^\\[(.*)\\]$", "$1")
  replace_all("$i.location[].*", "\\s?[,:;]$", "")
  replace_all("$i.publishedBy[].*", "^[©]|\\s?[,:;/=]?$", "")
  call_macro("leapYearAndMonthLenghtChecker",date:"$i.startDate")
  call_macro("leapYearAndMonthLenghtChecker",date:"$i.endDate")
  uniq("$i.location[]")
end


# 246 - Varying Form of Title (R) - $a - Title proper/short title (NR)

add_array("titleKeyword[]")
do list(path: "24610", "var": "$i")
  copy_field("$i.a","titleKeyword[].$append")
end


# 210 - Abbreviated Title (R) - $a - Abbreviated title (NR)
add_array("shortTitle[]")
do list(path: "210??", "var": "$i")
  copy_field("$i.a","shortTitle[].$append")
end

do list(path: "030??", "var": "$i")
  copy_field("$i.a","shortTitle[].$append")
end
