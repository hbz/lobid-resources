#   <!-- 655 Form/Genre keyword transformed as natureOfContent   -->

set_array("natureOfContent[]")


# 655 - Index Term-Genre/Form (R),  Subfield: $a (NR), $0 (R)
# Ind: 2. 7= Source specified in subfield $2.

do list(path:"655 7", "var":"$i")
 unless in("$i.a","natureOfContent[].*.label")
    copy_field("$i.a","natureOfContent[].$append.label")
    do list(path:"$i.0","var":"$j")
      # This is only DNB, should we also check for other uris for keywords?
      if any_match("$j","^\\(DE-588\\)(.*)$")
        copy_field("$j","natureOfContent[].$last.id")
        replace_all("natureOfContent[].$last.id", "^\\(DE-588\\)(.*)$", "http://d-nb.info/gnd/$1")
      end
    end
  end
end

# 689 - not MARC standard element/DNB specific keywords (R), Subfields: $A (seems NR)

do list(path:"689??", "var":"$i")
  unless in("$i.a","natureOfContent[].*.label")
    if any_equal("$i.A","f")
      copy_field("$i.a","natureOfContent[].$append.label")
    end
  end
end

set_array("subject[]")


# I change the approach from element oriented (all 630) to a subject/concept kind oriented approach (all LCSH, all GND, all keywords, etc.)

# 610 - Subject Added Entry-Corporate Name (R), Subfield: $a (NR)
# 650 - Subject Added Entry-Topical Term (R),  Subfield: $a (NR)
# 653 - Index Term-Uncontrolled (R),  !!! Subfield: $a (R) !!!!


do list(path:"610?4|650?4|653??", "var":"$i")
      # TODO: Deduplicate values.
  set_array("subject[].$append.type[]","Keyword")
  add_field("subject[].$last.source.label","Freie Verschlagwortung")
  add_field("subject[].$last.source.id","https://www.wikidata.org/wiki/Q47524318")
  copy_field("$i.a","subject[].$last.label")
end

do list(path:"610?0|650?0", "var":"$i")
      # TODO: Deduplicate values.
  set_array("subject[].$append.type[]","Concept")
  add_field("subject[].$last.source.label","Library of Congress Subject Headings")
  add_field("subject[].$last.source.id","https://id.loc.gov/authorities/subjects.html")
  copy_field("$i.a","subject[].$last.label")
end


do list(path:"610??|650??", "var":"$i")
  if any_match("$i.0","^\\(DE-588\\)(.*)$")
        # TODO: Deduplicate values.
    set_array("subject[].$append.type[]","Concept")
    add_field("subject[].$last.source.label","Gemeinsame Normdatei (GND)")
    add_field("subject[].$last.source.id","https://d-nb.info/gnd/7749153-1")
    copy_field("$i.a","subject[].$last.label")
    do list(path:"$i.0", "var":"$j")
      if any_match("$j","^\\(DE-588\\)(.*)$")
        copy_field("$j", "subject[].$last.id")
        replace_all("subject[].$last.id","^\\(DE-588\\)(.*)$","http://d-nb.info/gnd/$1")
        copy_field("$j", "subject[].$last.gndIdentifier")
        replace_all("subject[].$last.gndIdentifier","^\\(DE-588\\)(.*)$","$1")
      end
    end
  end
end


# 082 - Dewey Decimal Classification Number (R) - Subfield: $a (R), $2 (NR)

do list(path:"0820 |08200", "var":"$i")
  set_array("subject[].$append.type[]","Concept")
  add_field("subject[].$last.source.label","Dewey-Dezimalklassifikation")
  add_field("subject[].$last.source.id","http://d-nb.info/gnd/4149423-4")
  do list(path:"$i.a", "var": "$j")
    unless exists("subject[].$last.label")
      copy_field("$i.a","subject[].$last.label")
      lookup("subject[].$last.label", "$[deweyLabels]","sep_char":"\t")
      copy_field("$i.a","subject[].$last.notation")
    end
  end
  copy_field("$i.2","subject[].$last.version")
end

# 084 - Other Classification Number (R) - Subfield: $a (R), $2 (NR)

do list(path:"084??", "var":"$i")
  do list(path:"$i.0", "var":"$j")
    if any_contain("$j","https://nwbib.de/subjects")
      copy_field("$j", "subject[].$append.id")
      copy_field("$i.a", "subject[].$last.label")
      if any_match("$j", "https://nwbib.de/subjects#N(.*)$")
        copy_field("$j", "subject[].$last.notation")
        replace_all("subject[].$last.notation","https://nwbib.de/subjects#N(.*)$","$1")
      end
      set_array("subject[].$last.type[]","Concept")
      add_field("subject[].$last.source.id","https://nwbib.de/subjects")
      add_field("subject[].$last.source.label","Sachsystematik der Nordrhein-Westfälischen Bibliographie")
    end
  end
end

# local subjects 982 - no info on repeatability

do list(path:"982  ", "var":"$i")
  if exists("$i.a")
    set_array("subject[].$append.type[]","Keyword")
    copy_field("$i.M","$i.@organisations")
    lookup("$i.@organisations","alma-iz-code-to-isil")
    paste("subject[].$last.source.id","~http://lobid.org/organisations/","$i.@organisations","~#!", join_char:"")
    paste("subject[].$last.source.label","~Freie Verschlagwortung durch","$i.@organisations")
    copy_field("$i.a","subject[].$last.label")
    copy_field("$i.b","subject[].$last.label")
    copy_field("$i.2","subject[].$last.version")
  elsif exists("$i.b")
    set_array("subject[].$append.type[]","Keyword")
    copy_field("$i.M","$i.@organisations")
    lookup("$i.@organisations","alma-iz-code-to-isil")
    paste("subject[].$last.source.id","~http://lobid.org/organisations/","$i.@organisations","~#!", join_char:"")
    paste("subject[].$last.source.label","~Freie Verschlagwortung durch","$i.@organisations")
    copy_field("$i.a","subject[].$last.label")
    copy_field("$i.b","subject[].$last.label")
    copy_field("$i.2","subject[].$last.version")
  end
end


# 689 RSWK Schlagwortfolgen fka: Schlagwortketten 1 - 10 - no info on repeatability
do once("schlagwortfolgeMacro")
  do put_macro("schlagwortfolge")
    if exists("$[field]")
      set_array("subject[].$append.type[]","ComplexSubject")
      set_array("subject[].$last.label")
      set_array("subject[].$last.componentList[]")
      do list(path:"$[field]", "var":"$i")
        set_array("subject[].$last.componentList[].$append.type[]")
        do list(path: "$i.D", "var": "$k")
          copy_field("$k","subject[].$last.componentList[].$last.type[].$append")
        end
        copy_field("$i.a","subject[].$last.componentList[].$last.label")
        copy_field("$i.a","subject[].$last.label.$append")
        do list(path:"$i.0", "var": "$j")
          if any_match("$j","^\\(DE-588\\)(.*)$")
            add_field("subject[].$last.componentList[].$last.source.label","Gemeinsame Normdatei (GND)")
            add_field("subject[].$last.componentList[].$last.source.id","https://d-nb.info/gnd/7749153-1")
            copy_field("$j", "subject[].$last.componentList[].$last.id")
            replace_all("subject[].$last.componentList[].$last.id","^\\(DE-588\\)(.*)$","http://d-nb.info/gnd/$1")
            copy_field("$j", "subject[].$last.componentList[].$last.gndIdentifier")
            replace_all("subject[].$last.componentList[].$last.gndIdentifier","^\\(DE-588\\)(.*)$","$1")
          end
        end
      end
      join_field("subject[].$last.label"," | ")
    end
  end
end

# 689 RSWK Schlagwortfolgen fka: Schlagwortketten 1 - 10
call_macro("schlagwortfolge", field: "6890?")
call_macro("schlagwortfolge", field: "6891?")
call_macro("schlagwortfolge", field: "6892?")
call_macro("schlagwortfolge", field: "6893?")
call_macro("schlagwortfolge", field: "6894?")
call_macro("schlagwortfolge", field: "6895?")
call_macro("schlagwortfolge", field: "6896?")
call_macro("schlagwortfolge", field: "6897?")
call_macro("schlagwortfolge", field: "6898?")
call_macro("schlagwortfolge", field: "6899?")


lookup("subject[].*.componentList[].*.type[].*","rswk-indicator")


set_array("subjectAltLabel[]")
copy_field("GST  .a","subjectAltLabel[].$append")
copy_field("GST  .g","subjectAltLabel[].$append")
copy_field("GPN1 .a","subjectAltLabel[].$append")
copy_field("GGN  .a","subjectAltLabel[].$append")
uniq("subjectAltLabel[]")

# TODO: any way to add the labels to the matching subject ?

# nwbib

set_array("spatial[]")

do list(path:"084??", "var":"$i")
  do list(path:"$i.0", "var":"$j")
    if any_contain("$j","https://nwbib.de/spatial#")
      copy_field("$j", "spatial[].$append.id")
      copy_field("$i.a", "spatial[].$last.label")
      if any_match("$j", "https://nwbib.de/spatial#N(.*)$")
        copy_field("$j", "spatial[].$last.notation")
      end
      set_array("spatial[].$last.type[]","Concept")
      add_field("spatial[].$last.source.id","https://nwbib.de/spatial")
      add_field("spatial[].$last.source.label","Raumsystematik der Nordrhein-Westfälischen Bibliographie")
      copy_field("$j", "spatial[].$append.focus.id")
    end
  end
end

do list(path:"spatial[]", "var":"$i")
  replace_all("$i.notation","https://nwbib.de/spatial#N(.*)$","$1")
  lookup("$i.focus.id","src/main/resources/nwbib-spatial.tsv", sep_char:"\t")
  copy_field("$i.focus.id","$i.focus.label")
  replace_all("$i.focus.label","^(.*?)\\|.*\\|.*","$1")
  replace_all("$i.focus.id","^.*\\|(http.*)","$1")
  set_array("$i.focus.type[]")
  copy_field("$i.focus.id","$i.focus.type[].$append")
  lookup("$i.focus.type[].*","src/main/resources/wd_itemLabelTypesCoordinates.tsv", sep_char:"\t", delete:"true")
  copy_field("$i.focus.type[].$last", "$i.focus.geo.lat")
  copy_field("$i.focus.type[].$last", "$i.focus.geo.lon")
  replace_all("$i.focus.type[].*","^.*,(http.*),Point.*","$1")
  replace_all("$i.focus.geo.lat","^.*,Point\\((.*) .*\\)","$1")
  replace_all("$i.focus.geo.lon","^.*,Point\\(.* (.*)\\)","$1")
end
