# natureOfContent combines Formschlagwörter (`689 $A: f`) and Formangaben (`655`)

set_array("natureOfContent[]")


# 655 - Index Term-Genre/Form (R),  Subfield: $a (NR), $0 (R)
# Ind: 2. 7= Source specified in subfield $2.

do list(path:"655 7", "var":"$i")
 unless in("$i.a","natureOfContent[].*.label")
    unless any_equal("$i.2","gnd-carrier")
      unless any_match("$i.a",".*(Audiovisuelles Material|Bildplatte|CD|CD-ROM|Dia|Diskette|DVD-Audio|DVD-ROM|DVD-Video|Elektronische Publikation|Film|Medienkombination|Mikroform|Musikdruck|Online-Publikation|Schallplatte|Text|Tonbildreihe|Tonkassette|Tonträger|Videokassette).*")
        copy_field("$i.a","natureOfContent[].$append.label")
        do list(path:"$i.0","var":"$j")
          # This is only DNB, should we also check for other uris for keywords?
          if any_match("$j","^\\(DE-588\\)(.*)$")
            copy_field("$j","natureOfContent[].$last.id")
            replace_all("natureOfContent[].$last.id", "^\\(DE-588\\)(.*)$", "https://d-nb.info/gnd/$1")
          end
        end
      end
    end
  end
end

# 689 - not MARC standard element/DNB specific keywords (R), Subfields: $A (seems NR)

do list(path:"689??", "var":"$i")
  unless in("$i.a","natureOfContent[].*.label")
    if any_match("$i.A","f|F")
      unless any_match("$i.a",".*(Audiovisuelles Material|Bildplatte|CD|CD-ROM|Dia|Diskette|DVD-Audio|DVD-ROM|DVD-Video|Elektronische Publikation|Film|Medienkombination|Mikroform|Musikdruck|Online-Publikation|Schallplatte|Text|Tonbildreihe|Tonkassette|Tonträger|Videokassette).*")
        copy_field("$i.a","natureOfContent[].$append.label")
      end
    end
  end
end

# maps missing GND URIs to Formschlagwörtern with matching Formangaben-Values: https://wiki.dnb.de/download/attachments/106042227/AH-007.pdf
do list(path: "natureOfContent[]", "var":"$i")
  unless exists("$i.id")
    copy_field("$i.label","$i.id")
    lookup("$i.id","formschlagwort2Gnd", delete:"true")
    prepend("$i.id", "https://d-nb.info/gnd/")
  end
end

do once("subjectLabel")
  do put_macro("subjectLabel")
    set_array("subject[].$last.label")
    set_array("$i.@name")
    copy_field("$i.a","$i.@name.$append")
    copy_field("$i.b","$i.@name.$append")
    copy_field("$i.c","$i.@name.$append")
    copy_field("$i.d","$i.@name.$append")
    join_field("$i.@name")
    copy_field("$i.@name","subject[].$last.label.$append")
    copy_field("$i.x","subject[].$last.label.$append")
    copy_field("$i.y","subject[].$last.label.$append")
    copy_field("$i.z","subject[].$last.label.$append")
    copy_field("$i.v","subject[].$last.label.$append")
    join_field("subject[].$last.label"," / ")
  end
end

set_array("subject[]")


# I change the approach from element oriented (all 630) to a subject/concept kind oriented approach (all LCSH, all GND, all keywords, etc.)
# 600 - Subject Added Entry - Personal Name (R)
# 610 - Subject Added Entry-Corporate Name (R), Subfield: $a (NR)
# 611 - Subject Added Entry - Meeting Name (R)
# 630 - Subject Added Entry - Uniform Title (R) 
# 648 - Subject Added Entry - Chronological Term (R) 
# 650 - Subject Added Entry-Topical Term (R),  Subfield: $a (NR)
# 651 - Subject Added Entry - Geographic Name (R)
# 653 - Index Term-Uncontrolled (R),  !!! Subfield: $a (R) !!!!
# 688 - Subject Added Entry - Type of Entity Unspecified (R)


do list(path:"600?4|610?4|611?4|630?4|648?4|650?4|651?4|653??|688??", "var":"$i")
  set_array("subject[].$append.type[]","Concept")
  add_field("subject[].$last.source.label","Freie Verschlagwortung")
  add_field("subject[].$last.source.id","https://www.wikidata.org/wiki/Q47524318")
  call_macro("subjectLabel")
end

do list(path:"600?0|610?0|611?0|630?0|648?0|650?0|651?0", "var":"$i")
  set_array("subject[].$append.type[]","Concept")
  add_field("subject[].$last.source.label","Library of Congress Subject Headings")
  add_field("subject[].$last.source.id","https://id.loc.gov/authorities/subjects.html")
  call_macro("subjectLabel")
end

do list(path:"600?7|610?7|611?7|630?7|648?7|650?7|651?7", "var":"$i")
  if any_match("$i.2",".*fast.*")
    set_array("subject[].$append.type[]","Concept")
    add_field("subject[].$last.source.label","FAST (Faceted Application of Subject Terminology)")
    add_field("subject[].$last.source.id","http://fast.oclc.org/")
    call_macro("subjectLabel")
  end
end

do list(path:"600?2|610?2|611?2|630?2|648?2|650?2|651?2", "var":"$i")
    set_array("subject[].$append.type[]","Concept")
    add_field("subject[].$last.source.label","Medical Subject Headings")
    add_field("subject[].$last.source.id","https://www.nlm.nih.gov/mesh/meshhome.html")
    call_macro("subjectLabel")
end

do list(path:"600?7|610?7|611?7|630?7|648?7|650?7|651?7", "var":"$i")
  if any_match("$i.2",".*(mesh|MeSH).*")
    set_array("subject[].$append.type[]","Concept")
    add_field("subject[].$last.source.label","Medical Subject Headings")
    add_field("subject[].$last.source.id","https://www.nlm.nih.gov/mesh/meshhome.html")
    call_macro("subjectLabel")
  end
end

# Macro for combinedLabel Person

do once("gndPersonCombinedLabel")
  do put_macro("gndPersonCombinedLabel")
      set_array("$[field].@combinedLabel")  # check if GND concept has combined variant
      copy_field("$[field].a","$[field].@combinedLabel.$append")
      copy_field("$[field].b","$[field].@combinedLabel.$append")
      join_field("$[field].@combinedLabel", " ") # This is the difference to gnd non person
      if exists("$[field].c")
        paste("$[field].@combinedLabel", "$[field].@combinedLabel", "~, ", "$[field].c", join_char:"")
      end
      if exists("$[field].g")
        paste("$[field].@combinedLabel", "$[field].@combinedLabel", "~ (", "$[field].g", "~)", join_char:"")
      end
      if exists("$[field].x")
        paste("$[field].@combinedLabel", "$[field].@combinedLabel", "~/","$[field].x")
      end
  end
end

do list(path:"600??", "var":"$i")
  if any_match("$i.0","^\\(DE-588\\)(.*)$")
    set_array("subject[].$append.type[]","Concept")
    add_field("subject[].$last.source.label","Gemeinsame Normdatei (GND)")
    add_field("subject[].$last.source.id","https://d-nb.info/gnd/7749153-1")
    call_macro("gndPersonCombinedLabel",field:"$i")
    copy_field("$i.@combinedLabel", "subject[].$last.label")
    do list(path:"$i.0", "var":"$j")
      if any_match("$j","^\\(DE-588\\)(.*)$")
        copy_field("$j", "subject[].$last.id")
        replace_all("subject[].$last.id","^\\(DE-588\\)(.*)$","https://d-nb.info/gnd/$1")
        copy_field("$j", "subject[].$last.gndIdentifier")
        replace_all("subject[].$last.gndIdentifier","^\\(DE-588\\)(.*)$","$1")
      end
      # GND idn as variable
      if exists("$i.B")
        do list(path: "$i.B","var":"$gnd")
          unless exists("subject[].$last.@gndIdn")
            copy_field("$gnd","subject[].$last.@gndIdn")
          end
        end
      elsif any_match("$j","^.*DNB\\|(.*)$")
        copy_field("$j", "subject[].$last.@gndIdn")
        replace_all("subject[].$last.@gndIdn", "^.*DNB\\|(.*)$","GND-$1")
      elsif any_match("$j","^\\(DE-101\\)(.*)$")
        copy_field("$j", "subject[].$last.@gndIdn")
        replace_all("subject[].$last.@gndIdn", "^\\(DE-101\\)(.*)$","GND-$1")
      end
    end
  end
end

# Macro for combinedLabel Non-Person

do once("gndNonPersonCombinedLabel")
  do put_macro("gndNonPersonCombinedLabel")
    set_array("$[field].@combinedLabel")  # check if GND concept has combined variant
    copy_field("$[field].a","$[field].@combinedLabel.$append")
    copy_field("$[field].b","$[field].@combinedLabel.$append")
    join_field("$[field].@combinedLabel", ". ") # This is the difference to gnd person
    if exists("$[field].c")
      paste("$[field].@combinedLabel", "$[field].@combinedLabel", "~, ", "$[field].c", join_char:"")
    end
    if exists("$[field].g")
      paste("$[field].@combinedLabel", "$[field].@combinedLabel", "~ (", "$[field].g", "~)", join_char:"")
    end
    if exists("$[field].x")
      paste("$[field].@combinedLabel", "$[field].@combinedLabel", "~/","$[field].x")
    end
    if exists("$[field].t")
      paste("$[field].@combinedLabel", "$[field].@combinedLabel", "~: ","$[field].t", join_char:"")
    end
  end
end

do list(path:"610??|611??|630??|648??|650??|651??", "var":"$i")
  if any_match("$i.0","^\\(DE-588\\)(.*)$")
    set_array("subject[].$append.type[]","Concept")
    add_field("subject[].$last.source.label","Gemeinsame Normdatei (GND)")
    add_field("subject[].$last.source.id","https://d-nb.info/gnd/7749153-1")
    call_macro("gndNonPersonCombinedLabel",field:"$i")
    copy_field("$i.@combinedLabel", "subject[].$last.label")
    do list(path:"$i.0", "var":"$j")
      if any_match("$j","^\\(DE-588\\)(.*)$")
        copy_field("$j", "subject[].$last.id")
        replace_all("subject[].$last.id","^\\(DE-588\\)(.*)$","https://d-nb.info/gnd/$1")
        copy_field("$j", "subject[].$last.gndIdentifier")
        replace_all("subject[].$last.gndIdentifier","^\\(DE-588\\)(.*)$","$1")
      end
      # GND idn as variable
      if exists("$i.B")
        do list(path: "$i.B","var":"$gnd")
          unless exists("subject[].$last.@gndIdn")
            copy_field("$gnd","subject[].$last.@gndIdn")
          end
        end
      elsif any_match("$j","^.*DNB\\|(.*)$")
        copy_field("$j", "subject[].$last.@gndIdn")
        replace_all("subject[].$last.@gndIdn", "^.*DNB\\|(.*)$","GND-$1")
      elsif any_match("$j","^\\(DE-101\\)(.*)$")
        copy_field("$j", "subject[].$last.@gndIdn")
        replace_all("subject[].$last.@gndIdn", "^\\(DE-101\\)(.*)$","GND-$1")
      end
    end
  end
end


# compare GND identifier (idn) with ALMA GND enrichment elements:
do list(path:"subject[]", "var":"$i")
  set_array("$i.altLabel[]")
  do list(path:"GPN??", "var": "$z")
    if in ("$i.@gndIdn", "$z.B") # Person labels have no character between $a (Name) and $b (Number).
      call_macro("gndPersonCombinedLabel",field:"$z")
      copy_field("$z.@combinedLabel", "$i.altLabel[].$append")
    end
  end
  do list(path:"GEL??|GKT??|GKS??|GST??|GGN??", "var": "$z")  # Other altLabels have a "," character between $a and $b.
    unless any_equal("$z.i","Spitzenorgan")
      if in ("$i.@gndIdn", "$z.B")
        call_macro("gndNonPersonCombinedLabel",field:"$z")
        copy_field("$z.@combinedLabel", "$i.altLabel[].$append")
      end
    end
  end
  remove_field("$i.@gndIdn")
end

# 082 - Dewey Decimal Classification Number (R) - Subfield: $a (R), $2 (NR)

do list(path:"0820 |08200", "var":"$i")
  set_array("subject[].$append.type[]","Concept")
  add_field("subject[].$last.source.label","Dewey-Dezimalklassifikation")
  add_field("subject[].$last.source.id","https://d-nb.info/gnd/4149423-4")
  do list(path:"$i.a", "var": "$j")
    unless exists("subject[].$last.label")
      copy_field("$i.a","subject[].$last.label")
      lookup("subject[].$last.label", "$[deweyLabels]","sep_char":"\t")
      copy_field("$i.a","subject[].$last.notation")
    end
  end
  copy_field("$i.2","subject[].$last.version")
end

# 084 - Other Classification Number (R) - Subfield: $a (R), $2 (NR)

do list(path:"084??", "var":"$i")
  do list(path:"$i.0", "var":"$j")
    if any_contain("$j","https://nwbib.de/subjects")
      copy_field("$j", "subject[].$append.id")
      copy_field("$i.a", "subject[].$last.label")
      if any_match("$j", "https://nwbib.de/subjects#N(.*)$")
        copy_field("$j", "subject[].$last.notation")
        replace_all("subject[].$last.notation","https://nwbib.de/subjects#N(.*)$","$1")
      end
      set_array("subject[].$last.type[]","Concept")
      add_field("subject[].$last.source.id","https://nwbib.de/subjects")
      add_field("subject[].$last.source.label","Sachsystematik der Nordrhein-Westfälischen Bibliographie")
    end
  end
  unless any_contain("$i.0", "https://nwbib.de/") # filter out any nwbib concepts
    unless any_match("$i.2", "ssgn|fid") # filter out any Sondersammelgebiete and Fachinformationsdienste
      do list(path:"$i.a", "var":"$j")
        copy_field("$j", "subject[].$append.notation")
        set_array("subject[].$last.type[]","Concept")
         # LBZ-Notationen aka rpb2 fka RPB-Sachgruppen und Zusätze
        if any_match("$i.2", "rpb")
          if any_match("$j","\\d{3}")
            copy_field("$j","$i.@rpb2NotationId")
            prepend("$i.@rpb2NotationId","https://w3id.org/lobid/rpb2#n")
            copy_field("$i.@rpb2NotationId","subject[].$last.id")
            lookup("$i.@rpb2NotationId","rpb2")
            move_field("$i.@rpb2NotationId","subject[].$last.label")
            add_field("subject[].$last.source.id", "https://w3id.org/lobid/rpb2")
            add_field("subject[].$last.source.label", "LBZ-Notationen")
          # RPB-Raumsystematik aka rpbr
          elsif any_match("$j","^rpbr.*")
            copy_field("$j","$i.@rpbrNotationId")
            replace_all("$i.@rpbrNotationId","rpbr_(.*)_","https://rpb.lobid.org/spatial#n$1")
            copy_field("$i.@rpbrNotationId","subject[].$last.id")
            lookup("$i.@rpbrNotationId","rpbr")
            move_field("$i.@rpbrNotationId","subject[].$last.label")
            add_field("subject[].$last.source.id", "https://rpb.lobid.org/spatial")
            add_field("subject[].$last.source.label", "RPB-Raumsystematik")
            replace_all("subject[].$last.notation","rpbr_(.*)_","$1")
          # RPB-Sachsystematik fka. Systematik der Rheinland-Pfälzischen Bibliographie
          elsif any_match("$j","^rpb.*")
            copy_field("$j","$i.@rpbNotationId")
            replace_all("$i.@rpbNotationId","rpb(.*)","http://purl.org/lobid/rpb#n$1")
            copy_field("$i.@rpbNotationId","subject[].$last.id")
            lookup("$i.@rpbNotationId","rpb")
            move_field("$i.@rpbNotationId","subject[].$last.label")
            add_field("subject[].$last.source.id", "http://purl.org/lobid/rpb")
            add_field("subject[].$last.source.label", "RPB-Sachsystematik")
            replace_all("subject[].$last.notation","rpb(.*)","$1")
          end
        else
          copy_field("$i.2", "subject[].$last.source.label")
          lookup("subject[].$last.source.label","classificationCode2Label")
          copy_field("$i.2","subject[].$last.source.id")
          lookup("subject[].$last.source.id","classificationCode2Uri", delete:"true")
        end
      end
    end
  end
end




# local subjects 982 - no info on repeatability

do list(path:"982  ", "var":"$i")
  if exists("$i.a")
    set_array("subject[].$append.type[]","Keyword")
    copy_field("$i.M","$i.@organisations")
    lookup("$i.@organisations","alma-iz-code-to-isil")
    paste("subject[].$last.source.id","~http://lobid.org/organisations/","$i.@organisations","~#!", join_char:"")
    paste("subject[].$last.source.label","~Freie Verschlagwortung durch","$i.@organisations")
    copy_field("$i.a","subject[].$last.label")
    copy_field("$i.b","subject[].$last.label")
    copy_field("$i.2","subject[].$last.version")
  elsif exists("$i.b")
    set_array("subject[].$append.type[]","Keyword")
    copy_field("$i.M","$i.@organisations")
    lookup("$i.@organisations","alma-iz-code-to-isil")
    paste("subject[].$last.source.id","~http://lobid.org/organisations/","$i.@organisations","~#!", join_char:"")
    paste("subject[].$last.source.label","~Freie Verschlagwortung durch","$i.@organisations")
    copy_field("$i.a","subject[].$last.label")
    copy_field("$i.b","subject[].$last.label")
    copy_field("$i.2","subject[].$last.version")
  end
end


# 689 RSWK Schlagwortfolgen fka: Schlagwortketten 1 - 10 - no info on repeatability
do once("schlagwortfolgeMacro")
  do put_macro("schlagwortfolge")
    if exists("$[field]")
      set_array("subject[].$append.type[]","ComplexSubject")
      set_array("subject[].$last.label")
      set_array("subject[].$last.componentList[]")
      do list(path:"$[field]", "var":"$i")
        set_array("subject[].$last.componentList[].$append.type[]")
        do list(path: "$i.D", "var": "$k")
          copy_field("$k","subject[].$last.componentList[].$last.type[].$append")
        end
        if any_equal("subject[].$last.componentList[].$last.type[]","p")
          call_macro("gndPersonCombinedLabel",field:"$i") 
        else
          call_macro("gndNonPersonCombinedLabel",field:"$i")
        end
        copy_field("$i.@combinedLabel", "subject[].$last.componentList[].$last.label")
        unless any_equal("subject[].$last.componentList[].$last.label","")
          copy_field("subject[].$last.componentList[].$last.label","subject[].$last.label.$append")
        end
        do list(path:"$i.0", "var": "$j")
          if any_match("$j","^\\(DE-588\\)(.*)$")
            add_field("subject[].$last.componentList[].$last.source.label","Gemeinsame Normdatei (GND)")
            add_field("subject[].$last.componentList[].$last.source.id","https://d-nb.info/gnd/7749153-1")
            copy_field("$j", "subject[].$last.componentList[].$last.id")
            replace_all("subject[].$last.componentList[].$last.id","^\\(DE-588\\)(.*)$","https://d-nb.info/gnd/$1")
            copy_field("$j", "subject[].$last.componentList[].$last.gndIdentifier")
            replace_all("subject[].$last.componentList[].$last.gndIdentifier","^\\(DE-588\\)(.*)$","$1")
          end
          # GND idn as variable
          if exists("$i.B")
            do list(path: "$i.B","var":"$gnd")
              unless exists("subject[].$last.componentList[].$last.@gndIdn")
                copy_field("$gnd","subject[].$last.componentList[].$last.@gndIdn")
              end
            end
          elsif any_match("$j","^.*DNB\\|(.*)$")
            copy_field("$j", "subject[].$last.componentList[].$last.@gndIdn")
            replace_all("subject[].$last.componentList[].$last.@gndIdn", "^.*DNB\\|(.*)$","GND-$1")
          elsif any_match("$j","^\\(DE-101\\)(.*)$")
              copy_field("$j", "subject[].$last.componentList[].$last.@gndIdn")
              replace_all("subject[].$last.componentList[].$last.@gndIdn", "^\\(DE-101\\)(.*)$","GND-$1")
          end
        end
        copy_field("$i.d","subject[].$last.componentList[].$last.dateOfBirthAndDeath") # dates will be differentiated later in the process
      end
      join_field("subject[].$last.label"," | ")
    end
  end
end

# 689 RSWK Schlagwortfolgen fka: Schlagwortketten 1 - 10
call_macro("schlagwortfolge", field: "6890?")
call_macro("schlagwortfolge", field: "6891?")
call_macro("schlagwortfolge", field: "6892?")
call_macro("schlagwortfolge", field: "6893?")
call_macro("schlagwortfolge", field: "6894?")
call_macro("schlagwortfolge", field: "6895?")
call_macro("schlagwortfolge", field: "6896?")
call_macro("schlagwortfolge", field: "6897?")
call_macro("schlagwortfolge", field: "6898?")
call_macro("schlagwortfolge", field: "6899?")


lookup("subject[].*.componentList[].*.type[].*","rswk-indicator")


do list (path: "subject[]", "var": "$i")
  do list(path: "$i.componentList[]", "var": "$j")
    if any_match("$j.dateOfBirthAndDeath", "^([012][0-9]{3})-.*")
      copy_field("$j.dateOfBirthAndDeath","$j.dateOfBirth")
      replace_all("$j.dateOfBirth", "^([012][0-9]{3})-.*","$1")
    elsif any_match("$j.dateOfBirthAndDeath", "^([1-9][0-9]{2})-.*")
      copy_field("$j.dateOfBirthAndDeath","$j.dateOfBirth")
      replace_all("$j.dateOfBirth", "^([1-9][0-9]{2})-.*","0$1")
    elsif any_match("$j.dateOfBirthAndDeath", "^([1-9][0-9])-.*")
      copy_field("$j.dateOfBirthAndDeath","$j.dateOfBirth")
      replace_all("$j.dateOfBirth", "^([1-9][0-9])-.*","00$1")
    elsif any_match("$j.dateOfBirthAndDeath", "^([0-9])-.*")
      copy_field("$j.dateOfBirthAndDeath","$j.dateOfBirth")
      replace_all("$j.dateOfBirth", "^([0-9])-.*","000$1")
    elsif any_match("$j.dateOfBirthAndDeath", "^v([012][0-9]{3})-.*")
      copy_field("$j.dateOfBirthAndDeath","$j.dateOfBirth")
      replace_all("$j.dateOfBirth", "^v([012][0-9]{3})-.*","-$1")
    elsif any_match("$j.dateOfBirthAndDeath", "^v([1-9][0-9]{2})-.*")
      copy_field("$j.dateOfBirthAndDeath","$j.dateOfBirth")
      replace_all("$j.dateOfBirth", "^v([1-9][0-9]{2})-.*","-0$1")
    elsif any_match("$j.dateOfBirthAndDeath", "^v([1-9][0-9])-.*")
      copy_field("$j.dateOfBirthAndDeath","$j.dateOfBirth")
      replace_all("$j.dateOfBirth", "^v([1-9][0-9])-.*","-00$1")
    elsif any_match("$j.dateOfBirthAndDeath", "^v([0-9])-.*")
      copy_field("$j.dateOfBirthAndDeath","$j.dateOfBirth")
      replace_all("$j.dateOfBirth", "^v([0-9])-.*","-000$1")
    end
    if any_match("$j.dateOfBirthAndDeath", ".*-([012][0-9]{3})$")
      copy_field("$j.dateOfBirthAndDeath","$j.dateOfDeath")
      replace_all("$j.dateOfDeath", ".*-([012][0-9]{3})$","$1")
    elsif any_match("$j.dateOfBirthAndDeath", ".*-([1-9][0-9]{2})$")
      copy_field("$j.dateOfBirthAndDeath","$j.dateOfDeath")
      replace_all("$j.dateOfDeath", ".*-([1-9][0-9]{2})$","0$1")
    elsif any_match("$j.dateOfBirthAndDeath", ".*-([1-9][0-9])$")
      copy_field("$j.dateOfBirthAndDeath","$j.dateOfDeath")
      replace_all("$j.dateOfDeath", ".*-([1-9][0-9])$","00$1")
    elsif any_match("$j.dateOfBirthAndDeath", ".*-([0-9])$")
      copy_field("$j.dateOfBirthAndDeath","$j.dateOfDeath")
      replace_all("$j.dateOfDeath", ".*-([0-9])$","000$1")
    elsif any_match("$j.dateOfBirthAndDeath", ".*-v([012][0-9]{3})$")
      copy_field("$j.dateOfBirthAndDeath","$j.dateOfDeath")
      replace_all("$j.dateOfDeath", ".*-v([012][0-9]{3})$","-$1")
    elsif any_match("$j.dateOfBirthAndDeath", ".*-v([1-9][0-9]{2})$")
      copy_field("$j.dateOfBirthAndDeath","$j.dateOfDeath")
      replace_all("$j.dateOfDeath", ".*-v([1-9][0-9]{2})$","-0$1")
    elsif any_match("$j.dateOfBirthAndDeath", ".*-v([1-9][0-9])$")
      copy_field("$j.dateOfBirthAndDeath","$j.dateOfDeath")
      replace_all("$j.dateOfDeath", ".*-v([1-9][0-9])$","-00$1")
    elsif any_match("$j.dateOfBirthAndDeath", ".*-v([0-9])$")
      copy_field("$j.dateOfBirthAndDeath","$j.dateOfDeath")
      replace_all("$j.dateOfDeath", ".*-v([0-9])$","-000$1")
    end
    unless any_match("$j.dateOfBirthAndDeath","^ca\\..*|.*Jh\\.")
      remove_field("$j.dateOfBirthAndDeath")
    end
    # compare GND identifier (idn) with ALMA GND enrichment elements:
    set_array("$j.altLabel[]")
    do list(path:"GPN??", "var": "$z")
      if in ("$j.@gndIdn", "$z.B") # Person labels have no character between $a (Name) and $b (Number).
        call_macro("gndPersonCombinedLabel",field:"$z") 
        copy_field("$z.@combinedLabel", "$j.altLabel[].$append")
      end
    end
    do list(path:"GEL??|GKT??|GKS??|GST??|GGN??", "var": "$z")  # Other altLabels have a "," character between $a and $b.
      unless any_equal("$z.i","Spitzenorgan")
        if in ("$j.@gndIdn", "$z.B")  # check if GND concept has combined variant
          call_macro("gndNonPersonCombinedLabel",field:"$z")
          copy_field("$z.@combinedLabel", "$j.altLabel[].$append")
        end
      end
    end
    remove_field("$j.@gndIdn")
  end
end
do list(path:"subject[]", "var": "$i")
  uniq("$i.altLabel[]")
  do list(path:"$i.componentList[]","var":"$j")
    uniq("$j.altLabel[]")
  end
end

uniq("subject[]")

# nwbib

set_array("spatial[]")

do list(path:"084??", "var":"$i")
  do list(path:"$i.0", "var":"$j")
    if any_contain("$j","https://nwbib.de/spatial#")
      copy_field("$j", "spatial[].$append.id")
      copy_field("$i.a", "spatial[].$last.label")
      if any_match("$j", "https://nwbib.de/spatial#N(.*)$")
        copy_field("$j", "spatial[].$last.notation")
      end
      set_array("spatial[].$last.type[]","Concept")
      add_field("spatial[].$last.source.id","https://nwbib.de/spatial")
      add_field("spatial[].$last.source.label","Raumsystematik der Nordrhein-Westfälischen Bibliographie")
      copy_field("$j", "spatial[].$last.focus.id")
    end
  end
end

do list(path:"spatial[]", "var":"$i")
  replace_all("$i.notation","https://nwbib.de/spatial#N(.*)$","$1")
  copy_field("$i.focus.id","$i.focus.label")
  set_array("$i.focus.type[]")
  copy_field("$i.focus.id","$i.focus.type[].$append")
  copy_field("$i.focus.id","$i.focus.@geoData")
  lookup("$i.focus.id","nwbibWikidataId",delete:"true")
  lookup("$i.focus.label","nwbibWikidataLabel",delete:"true")
  lookup("$i.focus.type[].*","nwbibWikidataTypes",delete:"true")
  lookup("$i.focus.@geoData","nwbibWikidataGeodata",delete:"true")
  copy_field("$i.focus.@geoData", "$i.focus.geo.lat")
  copy_field("$i.focus.@geoData", "$i.focus.geo.lon")
  remove_field("$i.focus.@geoData")
  if any_match("$i.focus.type[].*",'^.*,.*')
    split_field("$i.focus.type[].$last",", ")
    flatten("$i.focus.type[]")
  end
  replace_all("$i.focus.geo.lon","^Point\\((.*) .*\\)","$1")
  replace_all("$i.focus.geo.lat","^Point\\(.* (.*)\\)","$1")
end

set_array("subjectslabels[]")
do list(path:"subject[]","var":"$i")
  if any_match("$i.type[]","Concept")
    unless exists("$i.notation")
      copy_field("$i.label","subjectslabels[].$append")
    end
  end
  copy_field("$i.componentList[].*.label","subjectslabels[].$append")
end
uniq("subjectslabels[]")
