# natureOfContent combines Formschlagwörter (`689 $A: f`) and Formangaben (`655`)

add_array("natureOfContent[]")

# 348 - Notated Music Characteristics (R)

do list(path:"348  ", "var":"$i")
  do list(path:"$i.0","var":"$j")
    copy_field("$i.a", "natureOfContent[].$append.label")
    # This is only DNB, should we also check for other uris for keywords?
    if any_match("$j","^\\(DE-588\\)(.+)$")
      copy_field("$j","natureOfContent[].$last.id")
      replace_all("natureOfContent[].$last.id", "^\\(DE-588\\)(.+)$", "https://d-nb.info/gnd/$1")
    end
  end
end

# 655 - Index Term-Genre/Form (R),  Subfield: $a (NR), $0 (R)
# Ind: 2. 7= Source specified in subfield $2.

do list(path:"655 7", "var":"$i")
  unless exists("$i.a")
    copy_field("$i.0", "natureOfContent[].$append.label")
    do list(path: "natureOfContent[]", "var":"$i")
      replace_all("$i.label", "^\\(DE-588\\)(.+)$", "$1")
      lookup("$i.label", "Gnd2formschlagwort", delete:"true")
    end
  end
  unless in("$i.a","natureOfContent[].*.label")
    unless any_equal("$i.2","gnd-carrier")
      unless any_match("$i.a",".*(Audiovisuelles Material|Bildplatte|CD|CD-ROM|Dia|Diskette|DVD-Audio|DVD-ROM|DVD-Video|Elektronische Publikation|Film|Medienkombination|Mikroform|Musikdruck|Online-Publikation|Schallplatte|Text|Tonbildreihe|Tonkassette|Tonträger|Videokassette).*")
        copy_field("$i.a","natureOfContent[].$append.label")
        do list(path:"$i.0","var":"$j")
          # This is only DNB, should we also check for other uris for keywords?
          if any_match("$j","^\\(DE-588\\)(.+)$")
            copy_field("$j","natureOfContent[].$last.id")
            replace_all("natureOfContent[].$last.id", "^\\(DE-588\\)(.+)$", "https://d-nb.info/gnd/$1")
          end
        end
      end
    end
  end
end

# 689 - not MARC standard element/DNB specific keywords (R), Subfields: $A (seems NR)

do list(path:"689??", "var":"$i")
  unless in("$i.a","natureOfContent[].*.label")
    if any_match("$i.A","f|F")
      unless any_match("$i.a",".*(Audiovisuelles Material|Bildplatte|CD|CD-ROM|Dia|Diskette|DVD-Audio|DVD-ROM|DVD-Video|Elektronische Publikation|Film|Medienkombination|Mikroform|Musikdruck|Online-Publikation|Schallplatte|Text|Tonbildreihe|Tonkassette|Tonträger|Videokassette).*")
        copy_field("$i.a","natureOfContent[].$append.label")
      end
    end
  end
end

# maps missing GND URIs to Formschlagwörtern with matching Formangaben-Values: https://wiki.dnb.de/download/attachments/106042227/AH-007.pdf
do list(path: "natureOfContent[]", "var":"$i")
  unless exists("$i.id")
    if any_contain("$i.label","Periodical")
      add_field("$i.label","Zeitschrift")
    end
    copy_field("$i.label","$i.id")
    lookup("$i.id","formschlagwort2Gnd", delete:"true")
    prepend("$i.id", "https://d-nb.info/gnd/")
    unless exists("$i.id")
      remove_field("$i.label")
    end
  end
end

add_array("subject[]")


# I change the approach from element oriented (all 630) to a subject/concept kind oriented approach (all LCSH, all GND, all keywords, etc.)
# 600 - Subject Added Entry - Personal Name (R)
# 610 - Subject Added Entry-Corporate Name (R), Subfield: $a (NR)
# 611 - Subject Added Entry - Meeting Name (R)
# 630 - Subject Added Entry - Uniform Title (R)
# 648 - Subject Added Entry - Chronological Term (R)
# 650 - Subject Added Entry-Topical Term (R),  Subfield: $a (NR)
# 651 - Subject Added Entry - Geographic Name (R)
# 653 - Index Term-Uncontrolled (R),  !!! Subfield: $a (R) !!!!
# 688 - Subject Added Entry - Type of Entity Unspecified (R)


do list(path:"600?4|610?4|611?4|630?4|648?4|650?4|651?4|653??|688??", "var":"$i")
  add_array("subject[].$append.type[]","Concept")
  add_field("subject[].$last.source.label","Freie Verschlagwortung")
  add_field("subject[].$last.source.id","https://www.wikidata.org/wiki/Q47524318")
  call_macro("subjectLabel")
  call_macro("alternateGraphicRepresentationArrayOfObjects",targetArray:"subject[]",targetField:"",variable:"$i")
end

do list(path:"600?0|610?0|611?0|630?0|648?0|650?0|651?0", "var":"$i")
  add_array("subject[].$append.type[]","Concept")
  add_field("subject[].$last.source.label","Library of Congress Subject Headings")
  add_field("subject[].$last.source.id","https://id.loc.gov/authorities/subjects.html")
  call_macro("subjectLabel")
  call_macro("alternateGraphicRepresentationArrayOfObjects",targetArray:"subject[]",targetField:"",variable:"$i")
end

do list(path:"600?7|610?7|611?7|630?7|648?7|650?7|651?7", "var":"$i")
  if any_match("$i.2",".*fast.*")
    add_array("subject[].$append.type[]","Concept")
    add_field("subject[].$last.source.label","FAST (Faceted Application of Subject Terminology)")
    add_field("subject[].$last.source.id","http://fast.oclc.org/")
    call_macro("subjectLabel")
  call_macro("alternateGraphicRepresentationArrayOfObjects",targetArray:"subject[]",targetField:"",variable:"$i")
  end
end

do list(path:"600?2|610?2|611?2|630?2|648?2|650?2|651?2", "var":"$i")
    add_array("subject[].$append.type[]","Concept")
    add_field("subject[].$last.source.label","Medical Subject Headings")
    add_field("subject[].$last.source.id","https://www.nlm.nih.gov/mesh/meshhome.html")
    call_macro("subjectLabel")
  call_macro("alternateGraphicRepresentationArrayOfObjects",targetArray:"subject[]",targetField:"",variable:"$i")
end

do list(path:"600?7|610?7|611?7|630?7|648?7|650?7|651?7", "var":"$i")
  if any_match("$i.2",".*(mesh|MeSH).*")
    add_array("subject[].$append.type[]","Concept")
    add_field("subject[].$last.source.label","Medical Subject Headings")
    add_field("subject[].$last.source.id","https://www.nlm.nih.gov/mesh/meshhome.html")
    call_macro("subjectLabel")
  call_macro("alternateGraphicRepresentationArrayOfObjects",targetArray:"subject[]",targetField:"",variable:"$i")
  end
end



do list(path:"600??", "var":"$i")
  if any_match("$i.0","^\\(DE-588\\)(.+)$")
    add_array("subject[].$append.type[]","Concept")
    add_field("subject[].$last.source.label","Gemeinsame Normdatei (GND)")
    add_field("subject[].$last.source.id","https://d-nb.info/gnd/7749153-1")
    call_macro("gndPersonCombinedLabel",field:"$i")
    copy_field("$i.@combinedLabel", "subject[].$last.label")
    call_macro("alternateGraphicRepresentationArrayOfObjects",targetArray:"subject[]",targetField:"",variable:"$i")
    do list(path:"$i.0", "var":"$j")
      if any_match("$j","^\\(DE-588\\)(.+)$")
        upcase("$j") #gnd ids only have upper case X
        copy_field("$j", "subject[].$last.id")
        replace_all("subject[].$last.id","^\\(DE-588\\)(.+)$","https://d-nb.info/gnd/$1")
        copy_field("$j", "subject[].$last.gndIdentifier")
        replace_all("subject[].$last.gndIdentifier","^\\(DE-588\\)(.+)$","$1")
      end
      # GND idn as variable
      if exists("$i.B")
        do list(path: "$i.B","var":"$gnd")
          unless exists("subject[].$last.@gndIdn")
            copy_field("$gnd","subject[].$last.@gndIdn")
          end
        end
      elsif any_match("$j","^.*DNB\\|(.*)$")
        copy_field("$j", "subject[].$last.@gndIdn")
        replace_all("subject[].$last.@gndIdn", "^.*DNB\\|(.*)$","GND-$1")
      elsif any_match("$j","^\\(DE-101\\)(.+)$")
        copy_field("$j", "subject[].$last.@gndIdn")
        replace_all("subject[].$last.@gndIdn", "^\\(DE-101\\)(.+)$","GND-$1")
      end
    end
  end
end

do list(path:"611??", "var":"$i")
  if any_match("$i.0","^\\(DE-588\\)(.+)$")
    add_array("subject[].$append.type[]","Concept")
    add_field("subject[].$last.source.label","Gemeinsame Normdatei (GND)")
    add_field("subject[].$last.source.id","https://d-nb.info/gnd/7749153-1")
    call_macro("gndEventCombinedLabel",field:"$i")
    copy_field("$i.@combinedLabel", "subject[].$last.label")
    call_macro("alternateGraphicRepresentationArrayOfObjects",targetArray:"subject[]",targetField:"",variable:"$i")
    do list(path:"$i.0", "var":"$j")
      if any_match("$j","^\\(DE-588\\)(.+)$")
        upcase("$j") #gnd ids only have upper case X
        copy_field("$j", "subject[].$last.id")
        replace_all("subject[].$last.id","^\\(DE-588\\)(.+)$","https://d-nb.info/gnd/$1")
        copy_field("$j", "subject[].$last.gndIdentifier")
        replace_all("subject[].$last.gndIdentifier","^\\(DE-588\\)(.+)$","$1")
      end
      # GND idn as variable
      if exists("$i.B")
        do list(path: "$i.B","var":"$gnd")
          unless exists("subject[].$last.@gndIdn")
            copy_field("$gnd","subject[].$last.@gndIdn")
          end
        end
      elsif any_match("$j","^.*DNB\\|(.*)$")
        copy_field("$j", "subject[].$last.@gndIdn")
        replace_all("subject[].$last.@gndIdn", "^.*DNB\\|(.*)$","GND-$1")
      elsif any_match("$j","^\\(DE-101\\)(.+)$")
        copy_field("$j", "subject[].$last.@gndIdn")
        replace_all("subject[].$last.@gndIdn", "^\\(DE-101\\)(.+)$","GND-$1")
      end
    end
  end
end

do list(path:"610??|630??|648??|650??|651??", "var":"$i")
  if any_match("$i.0","^\\(DE-588\\)(.+)$")
    add_array("subject[].$append.type[]","Concept")
    add_field("subject[].$last.source.label","Gemeinsame Normdatei (GND)")
    add_field("subject[].$last.source.id","https://d-nb.info/gnd/7749153-1")
    call_macro("gndOtherCombinedLabel",field:"$i")
    copy_field("$i.@combinedLabel", "subject[].$last.label")
    call_macro("alternateGraphicRepresentationArrayOfObjects",targetArray:"subject[]",targetField:"",variable:"$i")
    do list(path:"$i.0", "var":"$j")
      if any_match("$j","^\\(DE-588\\)(.+)$")
        upcase("$j") #gnd ids only have upper case X
        copy_field("$j", "subject[].$last.id")
        replace_all("subject[].$last.id","^\\(DE-588\\)(.+)$","https://d-nb.info/gnd/$1")
        copy_field("$j", "subject[].$last.gndIdentifier")
        replace_all("subject[].$last.gndIdentifier","^\\(DE-588\\)(.+)$","$1")
      end
      # GND idn as variable
      if exists("$i.B")
        do list(path: "$i.B","var":"$gnd")
          unless exists("subject[].$last.@gndIdn")
            copy_field("$gnd","subject[].$last.@gndIdn")
          end
        end
      elsif any_match("$j","^.*DNB\\|(.*)$")
        copy_field("$j", "subject[].$last.@gndIdn")
        replace_all("subject[].$last.@gndIdn", "^.*DNB\\|(.*)$","GND-$1")
      elsif any_match("$j","^\\(DE-101\\)(.+)$")
        copy_field("$j", "subject[].$last.@gndIdn")
        replace_all("subject[].$last.@gndIdn", "^\\(DE-101\\)(.+)$","GND-$1")
      end
    end
  end
end


# compare GND identifier (idn) with ALMA GND enrichment elements:
do list(path:"subject[]", "var":"$i")
  add_array("$i.altLabel[]")
  do list(path:"GPN??", "var": "$z")
    if in ("$i.@gndIdn", "$z.B") # Person labels have no character between $a (Name) and $b (Number).
      call_macro("gndPersonCombinedLabel",field:"$z")
      copy_field("$z.@combinedLabel", "$i.altLabel[].$append")
    end
  end
  do list(path:"GKS??", "var": "$z")  # Other altLabels have a "," character between $a and $b.
    unless any_equal("$z.i","Spitzenorgan")
      if in ("$i.@gndIdn", "$z.B")
        call_macro("gndEventCombinedLabel",field:"$z")
        copy_field("$z.@combinedLabel", "$i.altLabel[].$append")
      end
    end
  end
  do list(path:"GEL??|GKT??|GKS??|GST??|GGN??", "var": "$z")  # Other altLabels have a "," character between $a and $b.
    unless any_equal("$z.i","Spitzenorgan")
      if in ("$i.@gndIdn", "$z.B")
        call_macro("gndOtherCombinedLabel",field:"$z")
        copy_field("$z.@combinedLabel", "$i.altLabel[].$append")
      end
    end
  end
  remove_field("$i.@gndIdn")
end

# 082 - Dewey Decimal Classification Number (R) - Subfield: $a (R), $2 (NR)

do list(path:"0820?", "var":"$i")
  do list(path:"$i.a", "var": "$j")
    add_array("subject[].$append.type[]","Concept")
    add_field("subject[].$last.source.label","Dewey-Dezimalklassifikation")
    add_field("subject[].$last.source.id","https://d-nb.info/gnd/4149423-4")
    copy_field("$j","subject[].$last.label")
    lookup("subject[].$last.label", "deweyLabels", delete:"true")
    copy_field("$j","subject[].$last.notation")
    copy_field("$i.2","subject[].$last.version")
  end
end

# 084 - Other Classification Number (R) - Subfield: $a (R), $2 (NR)

do list(path:"084??", "var":"$i")
  do list(path:"$i.0", "var":"$j")
    if any_contain("$j","https://nwbib.de/subjects")
      copy_field("$j", "subject[].$append.id")
      copy_field("$j","$i.@nwbibLabel")
      lookup("$i.@nwbibLabel","nwbib")
      copy_field("$i.@nwbibLabel", "subject[].$last.label")
      if any_match("$j", "https://nwbib.de/subjects#N(.*)$")
        copy_field("$j", "subject[].$last.notation")
        replace_all("subject[].$last.notation","https://nwbib.de/subjects#N(.*)$","$1")
      end
      add_array("subject[].$last.type[]","Concept")
      add_field("subject[].$last.source.id","https://nwbib.de/subjects")
      add_field("subject[].$last.source.label","Sachsystematik der Nordrhein-Westfälischen Bibliographie")
    # new RPB-Sachsystematik fka. Systematik der Rheinland-Pfälzischen Bibliographie version -- follows pattern of NWBib $$a holds label and $$0 holds URI
    elsif any_contain("$j","http://purl.org/lobid/rpb")
      copy_field("$j", "subject[].$append.id")
      copy_field("$j","$i.@rpbLabel")
      lookup("$i.@rpbLabel","rpb")
      copy_field("$i.@rpbLabel", "subject[].$last.label")
      add_array("subject[].$last.type[]","Concept")
      add_field("subject[].$last.source.id", "http://purl.org/lobid/rpb")
      add_field("subject[].$last.source.label", "RPB-Sachsystematik")
      copy_field("$j", "subject[].$last.notation")
      replace_all("subject[].$last.notation","http://purl.org/lobid/rpb#n(.*)$","$1")
    end
  end
  unless any_contain("$i.0", "https://nwbib.de/") # filter out any nwbib concepts
    unless any_match("$i.a", "^rpbr.*") # filter out any RPB Spatial
      unless any_match("$i.2", "z") # filter out any z Other - Classification
        do list(path:"$i.a", "var":"$j")
          copy_field("$j", "subject[].$append.notation")
          add_array("subject[].$last.type[]","Concept")
          # LBZ-Notationen fka rpb2 fka RPB-Sachgruppen und Zusätze
          if any_match("$i.2", "rpb")
            if any_match("$j","\\d{3}")
              copy_field("$j","$i.@lbz-notationenId")
              prepend("$i.@lbz-notationenId","https://w3id.org/lobid/rpb2#n")
              copy_field("$i.@lbz-notationenId","subject[].$last.id")
              lookup("$i.@lbz-notationenId","lbz-notationen")
              move_field("$i.@lbz-notationenId","subject[].$last.label")
              add_field("subject[].$last.source.id", "https://w3id.org/lobid/rpb2")
              add_field("subject[].$last.source.label", "LBZ-Notationen")
            # old RPB-Sachsystematik fka. Systematik der Rheinland-Pfälzischen Bibliographie Version -- $$a holds notation
            elsif any_match("$j","^rpb.*")
              copy_field("$j","$i.@rpbNotationId")
              replace_all("$i.@rpbNotationId","rpb(.*)","http://purl.org/lobid/rpb#n$1")
              copy_field("$i.@rpbNotationId","subject[].$last.id")
              lookup("$i.@rpbNotationId","rpb")
              move_field("$i.@rpbNotationId","subject[].$last.label")
              add_field("subject[].$last.source.id", "http://purl.org/lobid/rpb")
              add_field("subject[].$last.source.label", "RPB-Sachsystematik")
              replace_all("subject[].$last.notation","rpb(.*)","$1")
            end
          elsif any_match("$i.2", "sdnb|zdbs")
            if any_match("$j","\\d{3}(\\.\\d{1,3})?|[BKS]")
              if any_equal("$i.q","DE-600")
                add_field("subject[].$last.source.label","DDC-Sachgruppen der ZDB")
                add_field("subject[].$last.source.id","https://zeitschriftendatenbank.de/fileadmin/user_upload/ZDB/pdf/zdbformat/5080.pdf")
              elsif any_equal("$i.2","zdbs")
                add_field("subject[].$last.source.label","DDC-Sachgruppen der ZDB")
                add_field("subject[].$last.source.id","https://zeitschriftendatenbank.de/fileadmin/user_upload/ZDB/pdf/zdbformat/5080.pdf")
              else
                add_field("subject[].$last.source.label","Sachgruppen der DNB")
                add_field("subject[].$last.source.id","https://bartoc.org/en/node/20049")
              end
              copy_field("$j","subject[].$last.label")
              lookup("subject[].$last.label", "dnbSachgruppen")
              # Also use all DNB and ZDB Sachgruppen as DDC too.
              add_array("subject[].$append.type[]","Concept")
              add_field("subject[].$last.source.label","Dewey-Dezimalklassifikation")
              add_field("subject[].$last.source.id","https://d-nb.info/gnd/4149423-4")
              copy_field("$j","subject[].$last.label")
              lookup("subject[].$last.label", "deweyLabels")
              copy_field("$j","subject[].$last.notation")
              copy_field("$i.2","subject[].$last.version")
            elsif any_match("$j","\\d{2}")
              add_field("subject[].$last.source.label","Systematik der DNB (bis 2003)")
              add_field("subject[].$last.source.id","https://bartoc.org/en/node/18497")
            end
          elsif any_match("$i.2", "sswd")
            copy_field("$j","$i.@gnd-sc-notationenId")
            prepend("$i.@gnd-sc-notationenId","https://d-nb.info/standards/vocab/gnd/gnd-sc#")
            copy_field("$i.@gnd-sc-notationenId","subject[].$last.id")
            lookup("$i.@gnd-sc-notationenId","gnd-sc-notationen")
            move_field("$i.@gnd-sc-notationenId","subject[].$last.label")
            add_field("subject[].$last.source.label", "GND-Systematik")
            add_field("subject[].$last.source.id", "http://d-nb.info/standards/vocab/gnd/gnd-sc")
          else
            copy_field("$i.2", "subject[].$last.source.label")
            lookup("subject[].$last.source.label","classificationCode2Label")
            copy_field("$i.2","subject[].$last.source.id")
            lookup("subject[].$last.source.id","classificationCode2Uri", delete:"true") # currently the delete option is still broken
          end
        end
      end
    end
  end
end

do list(path: "subject[]","var":"$i") # Delete all notations without mapped source.
  if exists("$i.notation")
    unless exists("$i.source")
      remove_field("$i.notation")
      remove_field("$i.type[]")
    end
    unless any_contain("$i.source.id","http")
      remove_field("$i.notation")
      remove_field("$i.type[]")
      remove_field("$i.source")
    end
  end
end

# RVK via Culturegraph

copy_field("almaMmsId","@rvkNotations")
lookup("@rvkNotations","rvk",delete:"true")
split_field("@rvkNotations", ",")
do list(path: "@rvkNotations","var":"$i")
  copy_field("$i","subject[].$append.notation")
  add_array("subject[].$last.type[]","Concept")
  add_field("subject[].$last.source.label","RVK (Regensburger Verbundklassifikation)")
  add_field("subject[].$last.source.id","https://d-nb.info/gnd/4449787-8")
end

# 689 RSWK Schlagwortfolgen fka: Schlagwortketten 1 - 10 - no info on repeatability

call_macro("schlagwortfolge", field: "6890?")
call_macro("schlagwortfolge", field: "6891?")
call_macro("schlagwortfolge", field: "6892?")
call_macro("schlagwortfolge", field: "6893?")
call_macro("schlagwortfolge", field: "6894?")
call_macro("schlagwortfolge", field: "6895?")
call_macro("schlagwortfolge", field: "6896?")
call_macro("schlagwortfolge", field: "6897?")
call_macro("schlagwortfolge", field: "6898?")
call_macro("schlagwortfolge", field: "6899?")


lookup("subject[].*.componentList[].*.type[].*","rswk-indicator")


do list (path: "subject[]", "var": "$i")
  do list(path: "$i.componentList[]", "var": "$j")
    if any_match("$j.label",".+") # Due to special cataloguing there can be empty label-properties. That are cleaned up at the end. These need to be skipped.
      unless exists("$j.type[].1")
        add_array("$j.type[]","SubjectHeading") # type: SubjectHeading as Fallback.
      end
    end
    if any_equal("$j.type[]","ConferenceOrEvent")
      remove_field("$j.dateOfBirthAndDeath")
    else
      call_macro("dateOfBirthAndDeathTransformer",dateOfBirthAndDeath:"$j.dateOfBirthAndDeath",dateOfBirthTo:"$j.dateOfBirth", dateOfDeathTo:"$j.dateOfDeath")
    end
    unless any_match("$j.dateOfBirthAndDeath","^ca\\..*|.*Jh\\.")
      remove_field("$j.dateOfBirthAndDeath")
    end
    replace_all("$j.label","<<|>>","")
    # compare GND identifier (idn) with ALMA GND enrichment elements:
    add_array("$j.altLabel[]")
    do list(path:"GPN??", "var": "$z")
      if in ("$j.@gndIdn", "$z.B") # Person labels have no character between $a (Name) and $b (Number).
        call_macro("gndPersonCombinedLabel",field:"$z")
        copy_field("$z.@combinedLabel", "$j.altLabel[].$append")
      end
    end
    do list(path:"GEL??|GKT??|GKS??|GST??|GGN??", "var": "$z")  # Other altLabels have a "," character between $a and $b.
      unless any_equal("$z.i","Spitzenorgan")
        if in ("$j.@gndIdn", "$z.B")  # check if GND concept has combined variant
          call_macro("gndOtherCombinedLabel",field:"$z")
          copy_field("$z.@combinedLabel", "$j.altLabel[].$append")
        end
      end
    end
    remove_field("$j.@gndIdn")
  end
end
do list(path:"subject[]", "var": "$i")
  replace_all("$i.altLabel[].*","<<|>>","")
  uniq("$i.altLabel[]")
  do list(path:"$i.componentList[]","var":"$j")
    replace_all("$j.altLabel[].*","<<|>>","")
    uniq("$j.altLabel[]")
  end
end

# clean up subjects
replace_all("subject[].*.label","<<|>>","")
uniq("subject[]")

## RVK-Cleaner
do list(path:"subject[]", "var": "$i")
  if any_equal("$i.source.id","https://d-nb.info/gnd/4449787-8")
    unless any_match("$i.notation","(LD,)?[A-Z]([A-Z]( [0-9]+([a-z]|\\.[0-9]+)?( [A-Z][0-9]*)?)?)?( - [A-Z]([A-Z]( [0-9]+([a-z]|\\.[0-9]+)?( [A-Z][0-9]*)?)?)?)?")
      remove_field("$i.*")
    end
  end
end

# spatial

add_array("spatial[]")

do list(path:"084??", "var":"$i")
  do list(path:"$i.0", "var":"$j")
  # nwbib spatial
    if any_contain("$j","https://nwbib.de/spatial#")
      copy_field("$j", "spatial[].$append.id")
      copy_field("$j","$i.@nwbib-spatialLabel")
      lookup("$i.@nwbib-spatialLabel","nwbib-spatial")
      copy_field("$i.@nwbib-spatialLabel", "spatial[].$last.label")
      if any_match("$j", "https://nwbib.de/spatial#N(.*)$")
        copy_field("$j", "spatial[].$last.notation")
      end
      add_array("spatial[].$last.type[]","Concept")
      add_field("spatial[].$last.source.id","https://nwbib.de/spatial")
      add_field("spatial[].$last.source.label","Raumsystematik der Nordrhein-Westfälischen Bibliographie")
      copy_field("$j", "spatial[].$last.focus.id")
    # new RPB spatial version -- follows pattern of NWBib $$a holds label and $$0 holds URI
    elsif any_contain("$j","https://rpb.lobid.org/spatial#")
      copy_field("$j", "spatial[].$append.id")
      copy_field("$j","$i.@rpbrNotationId")
      lookup("$i.@rpbrNotationId","rpbr")
      copy_field("$i.@rpbrNotationId", "spatial[].$last.label")
      add_array("spatial[].$last.type[]","Concept")
      add_field("spatial[].$last.source.id", "https://rpb.lobid.org/spatial")
      add_field("spatial[].$last.source.label", "RPB-Raumsystematik")
      copy_field("$j", "spatial[].$last.notation")
      replace_all("spatial[].$last.notation","https://rpb.lobid.org/spatial#n(.*)$","$1")
    end
  end
  do list(path:"$i.a", "var":"$j")
    # old RPB spatial Verion -- $$a holds notation
    if any_match("$j","^rpbr.*")
      add_array("spatial[].$append.type[]","Concept")
      copy_field("$j","$i.@rpbrNotationId")
      replace_all("$i.@rpbrNotationId","rpbr_(99_o)?(.*)_","https://rpb.lobid.org/spatial#n$2")
      copy_field("$i.@rpbrNotationId","spatial[].$last.id")
      lookup("$i.@rpbrNotationId","rpbr")
      move_field("$i.@rpbrNotationId","spatial[].$last.label")
      add_field("spatial[].$last.source.id", "https://rpb.lobid.org/spatial")
      add_field("spatial[].$last.source.label", "RPB-Raumsystematik")
      replace_all("spatial[].$last.notation","rpbr_(99_o)?(.*)_","$2")
    end
  end
end

do list(path:"spatial[]", "var":"$i")
  if any_contain("$i.source.id","https://nwbib.de/spatial")
    replace_all("$i.notation","https://nwbib.de/spatial#N(.*)$","$1")
    copy_field("$i.focus.id","$i.focus.label")
    add_array("$i.focus.type[]")
    copy_field("$i.focus.id","$i.focus.type[].$append")
    copy_field("$i.focus.id","$i.focus.@geoData")
    lookup("$i.focus.id","nwbibWikidataId",delete:"true")
    lookup("$i.focus.label","nwbibWikidataLabel",delete:"true")
    lookup("$i.focus.type[].*","nwbibWikidataTypes",delete:"true")
    lookup("$i.focus.@geoData","nwbibWikidataGeodata",delete:"true")
    copy_field("$i.focus.@geoData", "$i.focus.geo.lat")
    copy_field("$i.focus.@geoData", "$i.focus.geo.lon")
    remove_field("$i.focus.@geoData")
    if any_match("$i.focus.type[].*",'^.*,.*')
      split_field("$i.focus.type[].$last",", ")
      flatten("$i.focus.type[]")
    end
    replace_all("$i.focus.geo.lon","^Point\\((.*) .*\\)","$1")
    replace_all("$i.focus.geo.lat","^Point\\(.* (.*)\\)","$1")
  end
end

add_array("subjectslabels[]")
do list(path:"subject[]","var":"$i")
  if any_match("$i.type[]","Concept")
    unless exists("$i.notation")
      copy_field("$i.label","subjectslabels[].$append")
    end
  end
  copy_field("$i.componentList[].*.label","subjectslabels[].$append")
end
uniq("subjectslabels[]")
