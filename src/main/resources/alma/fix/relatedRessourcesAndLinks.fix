#
#  <entity name="sameAs[]" flushWith="record">
set_array("sameAs[]")


#    <entity name="">
#      <data name="id" source="035  .a">
#        <regexp match="\(DE-605\)(.*)" format="http://hub.culturegraph.org/resource/HBZ-${1}"/>
#      </data>
#      <data name="label" source="035  .a">
#        <regexp match="\(DE-605\)(.*)" format="Culturegraph Ressource"/>
#      </data>
#    </entity>


# 035 - System Control Number (R) - Subfield: $a (NR)
do list(path:"035  ", "var":"$i")
  if all_match("$i.a", "\\(DE-605\\)(.*)")
    copy_field("$i.a", "sameAs[].$append.id")
    replace_all("sameAs[].$last.id", "\\(DE-605\\)(.*)","http://hub.culturegraph.org/resource/HBZ-$1")
    add_field("sameAs[].$last.label","Culturegraph Ressource")
  end
end


#    <entity name="">
#      <data name="id" source="@oclcNumber">
#        <compose prefix="http://worldcat.org/oclc/"/>
#      </data>
#      <data name="label" source="@oclcNumber">
#        <constant value="OCLC Ressource"/>
#      </data>
#    </entity>

do list(path: "oclcNumber[]", "var": "$i")
  paste("sameAs[].$append.id","~http://worldcat.org/oclc/", "$i", join_char:"")
  add_field("sameAs[].$last.label","OCLC Ressource")
end


#    <entity name="">
#      <data name="id" source="@zdbId">
#        <compose prefix="http://ld.zdb-services.de/resource/"/>
#      </data>
#      <data name="label" source="@zdbId">
#        <constant value="ZDB-Ressource"/>
#      </data>
#    </entity>
#  </entity>

do list(path: "zdbId", "var": "$i")
  paste("sameAs[].$append.id","~http://ld.zdb-services.de/resource/", "$i", join_char:"")
  add_field("sameAs[].$last.label","ZDB-Ressource")
end

# 770 - Supplement/Special Issue Entry (R) - Subflied: $i (R), $t (NR), $w (R)

set_array("supplement[]")

do list(path:"77008", "var":"$i")
  if any_match("$i.i", "Beil.*|Supp.*")
    add_field("supplement[].$append.test","")
    copy_field("$i.t","supplement[].$last.label")
    copy_field("$i.w","supplement[].$last.id")
  end
end

replace_all("supplement[].*.id","^\\(DE-605\\)(.*)$","http://lobid.org/resources/$1#!")
replace_all("supplement[].*.id","^\\(DE-600\\)(.*)$","http://ld.zdb-services.de/resource/$1#!")

# isPartOf
# it describes the relation between a published ressource and its superordinate series or collection.
# in contrast to containedIn it is a standalone publication

#  <entity name="isPartOf[]" flushWith="record">
set_array("isPartOf[]")
#    <entity name="" sameEntity="true" reset="true" flushWith="773??|490??">
#      <data source="773??.w|490??.a" name="type">
#        <constant value="IsPartOfRelation" />
#      </data>
#      <entity name="hasSuperordinate[]" sameEntity="true" reset="true">
#        <entity name="" sameEntity="true" reset="true" flushWith="773??|490??">
#          <data name="id" source="773??.w">
#            <regexp match="\(DE-605\)(.*)" format="http://lobid.org/resources/${1}#!"/>
#          </data>
#          <data source="773??.t|490??.a" name="label" />
#        </entity>
#      </entity>
#      <data source="773??.q" name="numbering" />
#    </entity>

# 773 - Host Item Entry (R) Subfield: $w (NR), $t (NR), $q (NR)
# TODO: Shouldnt type always be arrays

unless any_match("leader", "^.{7}[ad].*")
  do list(path: "773??", "var": "$i")
    set_array("isPartOf[].$append.type[]", "IsPartOfRelation")
    set_array("isPartOf[].$last.hasSuperordinate[]")
    add_field("isPartOf[].$last.hasSuperordinate[].$append.test", "")
    if all_match("$i.w", "\\(DE-605\\)(.*)")
      copy_field("$i.w", "isPartOf[].$last.hasSuperordinate[].$append.id")
      replace_all("isPartOf[].$last.hasSuperordinate[].$last.id", "^\\(DE-605\\)(.*)$", "http://lobid.org/resources/$1#!")
    end
    copy_field("$i.t", "isPartOf[].$last.hasSuperordinate[].$last.label")
    copy_field("$i.q", "isPartOf[].$last.numbering")
  end
end

# 490 - Series Statement (R) - Subfield: $a (R)
# TODO: Shouldnt type always be arrays
# 490 with 1. Indicator 1 has an identical entry in 830. So only 490 with 1. Indicator 0

do list(path: "4900?", "var": "$i")
  set_array("isPartOf[].$append.type[]", "IsPartOfRelation")
  set_array("isPartOf[].$last.hasSuperordinate[]")
  copy_field("$i.a", "isPartOf[].$last.hasSuperordinate[].$append.label")
  copy_field("$i.v", "isPartOf[].$last.numbering")
end

#    <entity name="" sameEntity="true" reset="true" flushWith="830??">
#      <!-- Check for duplicates in local fields. -->
#      <if>
#        <data source="830??.w">
#          <unique/>
#        </data>
#      </if>
#      <data source="830??.w" name="type">
#        <constant value="IsPartOfRelation" />
#      </data>
#      <entity name="hasSuperordinate[]" sameEntity="true" reset="true">
#        <entity name="" sameEntity="true" reset="true" flushWith="830??">
#          <data name="id" source="830??.w">
#            <regexp match="\(DE-605\)(.*)" format="http://lobid.org/resources/${1}#!"/>
#          </data>
#          <data source="830??.a" name="label" />
#        </entity>
#      </entity>
#      <data source="830??.v" name="numbering" />
#    </entity>
#  </entity>

# 830 - Series Added Entry-Uniform Title (R) - Subfield: $w (R), $a (NR), $v (NR)
# Element can be repeatable with local entries they have subfield $M.

do list(path: "830??", "var": "$i")
  unless exists("$i.M")
    set_array("isPartOf[].$append.type[]", "IsPartOfRelation")
    set_array("isPartOf[].$last.hasSuperordinate[]")
    add_field("isPartOf[].$last.hasSuperordinate[].$append.test", "")
    if all_match("$i.w", "\\(DE-605\\)(.*)")
      copy_field("$i.w", "isPartOf[].$last.hasSuperordinate[].$last.id")
      replace_all("isPartOf[].$last.hasSuperordinate[].$last.id", "^\\(DE-605\\)(.*)$", "http://lobid.org/resources/$1#!")
    end
    copy_field("$i.a", "isPartOf[].$last.hasSuperordinate[].$last.label")
    copy_field("$i.v", "isPartOf[].$last.numbering")
  end
end

# containedIn
# containedIn only states ressources that are not published by them self but as part of a host ressource.

set_array("containedIn[]")

# 773 - Host Item Entry (R)  - $w  (R)

if any_match("leader", "^.{7}[ad].*")
  do list(path:"77308", "var":"$i")
    do list(path:"$i.w", "var":"$j")
      if any_match("$j","^\\(DE-(600|605)\\).*") # TODO: Some ressources have ALMA MMS IDs as relation idetifier e.g.: 991000688209706449 has $w 991021220959706449. How to deal with these?
        copy_field("$j","containedIn[].$append.id")
      end
    end
  end
end

replace_all("containedIn[].*.id", "^\\(DE-605\\)(.*)$", "http://lobid.org/resources/$1#!")
replace_all("containedIn[].*.id", "^\\(DE-600\\)(.*)$", "http://ld.zdb-services.de/resource/$1")

#
#  <!-- 8xx -->
#  <entity name="tableOfContents[]" flushWith="record">
#    <entity name="" sameEntity="true" reset="true">
#      <if>
#        <all flushWith="8564?" sameEntity="true" reset="true">
#          <data source="8564?.3">
#            <regexp match="^[Ii][Nn][Hh][aA][lL][tT][sS][vV]"/>
#          </data>
#          <data source="8564?.u">
#            <unique />
#          </data>
#        </all>
#      </if>
#      <data name="id" source="8564?.u" />
#      <data name="label" source="8564?.3" />
#    </entity>
#  </entity>

# 856 - Electronic Location and Access (R) - Subfield: $u (R) $3 (NR)
# 1. Indicator: 4 = HTTP

set_array("tableOfContents[]")

do list(path: "8564?", "var":"$i")
  unless exists("$i.M")
    if all_match("$i.3", "^[Ii][Nn][Hh][aA][lL][tT][sS][vV].*") # Inhaltsverzeichnis
      copy_field("$i.3", "tableOfContents[].$append.label")
      copy_field("$i.u", "tableOfContents[].$last.id")
    end
  end
end

set_array("description[]")

do list(path: "8564?", "var":"$i")
  unless exists("$i.M")
    if all_match("$i.3", "^[Ii][Nn][Hh][aA][lL][tT][sS][tT].*") # Inhaltstext
      copy_field("$i.3", "description[].$append.label")
      copy_field("$i.u", "description[].$last.id")
    end
  end
end

set_array("seeAlso[]")

do list(path: "8564?", "var":"$i")
  unless exists("$i.M")
    if all_match("$i.3", "^[zZ][uU][sS].*") # Zusätzliche Angaben
      copy_field("$i.3", "seeAlso[].$append.label")
      copy_field("$i.u", "seeAlso[].$last.id")
    end
  end
end

set_array("fulltextOnline[]")

do list(path: "8564?", "var":"$i")
  unless exists("$i.M")
    if all_equal("$i.z", "kostenfrei") # kostenfrei, added Digitalisierung not only Verlag or Agentur as filter
      if all_match("$i.x", "Verlag|Agentur|Digitalisierung")
        copy_field("$i.x", "fulltextOnline[].$append.label")
        copy_field("$i.u", "fulltextOnline[].$last.id")
      end
    end
    if all_match("$i.3", "^[vV][oO][lL].*") # Volltext
      copy_field("$i.3", "fulltextOnline[].$append.label")
      copy_field("$i.u", "fulltextOnline[].$last.id")
    end
    if all_match("$i.u", "^http:\\/\\/dx.doi.org\\/.*") # Volltext
      copy_field("$i.u", "fulltextOnline[].$append.id")
      add_field("fulltextOnline[].$last.label", "DOI-Link")
    end
    if all_match("$i.u", "^http:\\/\\/nbn-resolving.de\\/urn.*") # Volltext
      copy_field("$i.u", "fulltextOnline[].$append.id")
      add_field("fulltextOnline[].$last.label", "URN-Link")
    end
  end
end

# "fulltextOnline[]" needs to add:
#		<combine name="@fulltextOnlineUri" value="${a}" sameEntity="true">
#			<data source="655[-eu][ -1].[x3]">
#				<regexp match="Archivierte Online|EZB|Online-Ausg|Resolving-System|Volltext"/>
#			</data>
#			<data source="@linkUri" name="a"/>
#		</combine>

# TODO: hasVersion is outcommented since it needs some remodelling
# See https://github.com/hbz/lobid-resources/issues/1242
#  <!--
#  <entity name="hasVersion[]" flushWith="record">
#    <entity name="" flushWith="856??" sameEntity="true">
#      <if>
#        <any flushWith="856??">
#          <none flushWith="856??" sameEntity="true">
#            <data source="856??.M" />
#          </none>
#          <data source="856??.M">
#            <equals string="$[member]" />
#          </data>
#        </any>
#      </if>
#      <data name="id" source="856??.u">
#        <occurrence only="1" sameEntity="true" />
#        <unique />
#      </data>
#      <data name="label" source="856??.z">
#        <occurrence only="1" sameEntity="true" />
#      </data>
#    </entity>
#  </entity> -->
#



#  <entity name="related[]" flushWith="record">
#    <entity name="" flushWith="775??|776??" sameEntity="true">
#      <if>
#        <any>
#          <data source="775??.z|776??.z">
#            <unique />
#          </data>
#          <data source="775??.w|776??.w">
#            <unique />
#          </data>
#          <data source="775??.t|776??.t">
#            <unique />
#          </data>
#          <data source="775??.x|776??.x">
#            <unique />
#          </data>
#        </any>
#      </if>
#      <data name="id" source="775??.w|776??.w">
#        <regexp match="\(DE-605\)(.*)" format="http://lobid.org/resources/${1}#!"/>
#      </data>
#      <concat name="note" delimiter=" " sameEntity="true" flushWith="775??|776??">
#        <data source="775??.i|776??.i" />
#        <data source="775??.n|776??.n" />
#      </concat>
#      <data name="label" source="775??.t|776??.t" />
#      <entity name="isbn[]" sameEntity="true">
#        <data name="" source="775??.z|776??.z">
#          <isbn to="clean"/>
#          <isbn to="isbn10"/>
#          <unique />
#        </data>
#        <data name="" source="775??.z|776??.z">
#          <isbn to="clean"/>
#          <isbn to="isbn13"/>
#          <unique />
#        </data>
#      </entity>
#      <data name="issn" source="775??.x|776??.x" />
#    </entity>
#  </entity>

# 775 - Other Edition Entry (R) - Subfield: $w (R), $t (NR), $i (R), $n (R), $x(NR), $z (R)
# 776 - Additional Physical Form Entry (R) -  Subfield: $w (R), $t (NR), $i (R), $n (R), $x (NR), $z (R)
# TODO: Needs ISBN function for mapping ISBN in subfield Z! Need test ressource for that.

set_array("related[]")
do list(path:"775??|776??", "var":"$i")
  unless exists("$i.M")
    add_field("related[].$append.test","")
    if all_match("$i.w", "\\(DE-605\\)(.*)")
      copy_field("$i.w", "related[].$last.id")
      replace_all("related[].$last.id", "^\\(DE-605\\)(.*)$", "http://lobid.org/resources/$1#!")
    end
    paste("related[].$last.note","$i.i", "$i.n")
    copy_field("$i.t", "related[].$last.label")
    copy_field("$i.x", "related[].$last.issn")
    copy_field("$i.z", "$i.ISBN13")
    isbn("$i.isbn13", to: "clean")
    if any_match("$i.isbn13", ".{13}")
      copy_field("$i.isbn13", "$i.isbn10")
      isbn("$i.isbn10", to:"isbn10")
    elsif any_match("$i.isbn13", ".{10}")
      copy_field("$i.isbn13", "$i.isbn10")
      isbn("$i.isbn13", to:"isbn13")
    end
    set_array("related[].$last.isbn[]")
    copy_field("$i.isbn13|isbn10", "related[].$last.isbn[].$append")
  end
end






#
#  <!-- TODO: inCollection: missing edoweb, rpb, publisso, also type-element -->
#  <entity name="inCollection[]" flushWith="record">
set_array("inCollection[]")


#    <!-- ZDB -->
#    <entity name="" sameEntity="true" reset="true">
#      <data name="label" source="@zdbId">
#        <constant value="Zeitschriftendatenbank (ZDB)" />
#      </data>
#      <data name="id" source="@zdbId">
#        <constant value="http://lobid.org/resources/HT014846970#!" />
#      </data>
#      <entity name="type[]" sameEntity="true">
#        <data name="" source="@zdbId">
#          <constant value="Collection"/>
#        </data>
#      </entity>
#    </entity>

if exists("zdbId")
  add_field("inCollection[].$append.id", "http://lobid.org/resources/HT014846970#!")
  add_field("inCollection[].$last.label", "Zeitschriftendatenbank (ZDB)")
end

# rpb
# Checks rpb subjects are provided.

do list(path:"084??", "var":"$i")
  do once()
    if any_equal("$i.2","rpb")
      add_field("inCollection[].$append.id", "http://lobid.org/resources/HT013494180#!")
      add_field("inCollection[].$last.label", "Rheinland-Pfälzische Bibliographie")
    end
  end
end


#    <!-- EZB -->
#    <entity name="" sameEntity="true" reset="true">
#      <data name="label" source="85640.x">
#        <equals string="EZB" />
#        <constant value="Elektronische Zeitschriftenbibliothek (EZB)" />
#      </data>
#      <data name="id" source="85640.x">
#        <equals string="EZB" />
#        <constant value="http://lobid.org/resources/HT016356466#!" />
#      </data>
#      <entity name="type[]" sameEntity="true">
#        <data name="" source="85640.x">
#          <constant value="Collection"/>
#        </data>
#      </entity>
#    </entity>

# 856 - Electronic Location and Access (R) - Subfield: $x - Nonpublic note (R)
# TODO: Check if src/test/resources/alma-fix/(CKB)5280000000199164.xml is also an EZB titel even when it has no 865.
do list(path:"85640", "var":"$i")
  if any_equal("$i.x","EZB") # can test x and x.*
    add_field("inCollection[].$append.id", "http://lobid.org/resources/HT016356466#!")
    add_field("inCollection[].$last.label", "Elektronische Zeitschriftenbibliothek (EZB)")
  end
end


do list(path:"85640", "var":"$i")
  if any_match("$i.u","^.*edoweb.*") # can test x and x.*
    add_field("inCollection[].$append.id", "http://lobid.org/resources/HT016925914#!")
    add_field("inCollection[].$last.label", "Edoweb Rheinland-Pfalz")
  end
end


# TODO: AlephMorph checked for ellinet in "078r1.a" but publisso is also stated in the Link URI is that enough?
do list(path:"85640", "var":"$i")
  if any_match("$i.u","^.*publisso.*") # can test x and x.*
    add_field("inCollection[].$append.id", "http://repository.publisso.de")
    add_field("inCollection[].$last.label", "Fachrepositorium Lebenswissenschaften")
  end
end


#    <!-- nwbib -->
#    <entity name="" sameEntity="true" reset="true">
#      <data name="label" source="084  .2">
#        <equals string="nwbib" />
#        <constant value="Nordrhein-Westfälische Bibliographie (NWBib)" />
#        <unique />
#      </data>
#      <data name="id" source="084  .2">
#        <equals string="nwbib" />
#        <constant value="http://lobid.org/resources/HT014176012#!" />
#      </data>
#      <entity name="type[]" sameEntity="true">
#        <data name="" source="084  .2">
#          <constant value="Collection"/>
#        </data>
#      </entity>
#    </entity>

# 084 - Other Classification Number (R), Subfield: $2 (NR)

do list(path:"084  ", "var":"$i")
  if any_equal("$i.2","nwbib")
    unless any_equal("inCollection[].*.id","http://lobid.org/resources/HT014176012#!")
      add_field("inCollection[].$append.id", "http://lobid.org/resources/HT014176012#!")
      add_field("inCollection[].$last.label", "Nordrhein-Westfälische Bibliographie (NWBib)")
    end
  end
end



#    <!-- eResource package -->
#    <entity name="" sameEntity="true" reset="true">
#      <data source="912  .a" name="id">
#        <regexp match="(ZDB-[0-9]{1,6}-[a-zA-Z|0-9\\-]*).*" format="http://lobid.org/organisations/${1}#!"/>
#      </data>
#      <data source="912  .a" name="label">
#        <regexp match="(ZDB-[0-9]{1,6}-[a-zA-Z|0-9\\-]*).*" format="eResource package" />
#      </data>
#      <entity name="type[]" sameEntity="true" reset="true">
#        <data name="" source="912  .a">
#          <constant value="Collection"/>
#        </data>
#      </entity>
#    </entity>

# 912 ## "(nur Präfix ZDB-, und "vd18")" no Information about repeatability

do list(path:"912  ", "var":"$i")
  if any_match("$i.a", "(ZDB-[0-9]{1,6}-[a-zA-Z|0-9\\-]*).*")
    copy_field("$i.a", "inCollection[].$append.id")
    replace_all("inCollection[].$last.id", "(ZDB-[0-9]{1,6}-[a-zA-Z|0-9\\-]*).*", "http://lobid.org/organisations/$1#!")
    add_field("inCollection[].$last.label", "eResource package")
  end
end


#    <!-- E-Book and digital copy selections (inofficial) -->
#    <entity name="" sameEntity="true" reset="true">
#      <data source="962  .e" name="id">
#        <regexp match="^[^Z][^D][^B].*" />
#        <regexp match="(.*)" format="https://lobid.org/collections#${1}"/>
#      </data>
#      <entity name="type[]" sameEntity="true" reset="true">
#        <data name="" source="962  .e">
#          <constant value="Collection"/>
#        </data>
#      </entity>
#    </entity>

# 912 ## no Information about repeatability

do list(path:"962  ", "var":"$i")
  do list(path:"$i.e", "var": "$j")
    unless any_match("$j", "^ZDB.*")
      copy_field("$j", "inCollection[].$append.id")
      replace_all("inCollection[].$last.id", "^(.*)$", "http://lobid.org/collections/$1#!")
      # TODO: Do we need a label?
    end
  end
end

#    <!-- 960 selections (inofficial) -->
#    <entity name="" sameEntity="true" reset="true">
#      <data source="9601 .?" name="id">
#        <regexp match="(.*)" format="https://lobid.org/collections#${1}"/>
#      </data>
#      <entity name="type[]" sameEntity="true" reset="true">
#        <data name="" source="9601 .?">
#          <constant value="Collection"/>
#        </data>
#      </entity>
#    </entity>
#  </entity>
#</rules>
#

# 960 ## no Information about repeatability
# TODO: This needs further inspection if we need a collection fr all subfields: https://service-wiki.hbz-nrw.de/display/VDBE/960+-+Selektionskennzeichen+NZ
# Values from r can be invalid.

# do list(path:"960??", "var":"$i")
#   do list(path:"$i.?", "var": "$j")
#       copy_field("$j", "inCollection[].$append.id")
#       replace_all("inCollection[].$last.id", "^(.*)$", "http://lobid.org/collections/$1#!")
#       # TODO: Do we need a label? https://github.com/hbz/lobid-resources/issues/1305#issuecomment-912312471, also labels seem wrong.
#   end
# end


set_array("inCollection[].*.type[]","Collection")


