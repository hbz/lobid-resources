add_array("hasItem[]")
add_array("@ITM-H") # Helper element for creating Holding from HOL ("PhysikalischerTitel")
do list(path:"ITM  ", "var": "$i")
  call_macro("deSol1BridgeTester", holdingId: "$i.a")
  unless exists("$i.deSol1Bridge") # exclude DE-Sol1-Bridge items/holdings because we add them otherwise
    call_macro("suppressedLocation", targetField: "$i", libraryCodeSubfield: "w", locationCodeSubfield: "x")
    unless exists("$i.suppressedLocation") # Test if location is suppressed with mapping provided by the libraries.
      add_hash( "hasItem[].$append")
      add_field("hasItem[].$last.label", "lobid Bestandsressource")
      add_array("hasItem[].$last.type[]", "Item","PhysicalObject")
      if exists("$i.z") # Temporary call number subfield
        unless in("$i.x", "$i.v") # Current location != Permanent location
          copy_field("$i.z", "hasItem[].$last.callNumber")
        end
      end
      unless exists("hasItem[].$last.callNumber")
        if exists("$i.n") # Item call number subfield
          copy_field("$i.n", "hasItem[].$last.callNumber")
        else # $c = Call number subfield
          copy_field("$i.c", "hasItem[].$last.callNumber")
        end
      end
      copy_field("$i.B", "hasItem[].$last.inventoryNumber")
      copy_field("$i.H", "@ITM-H.$append")
      copy_field("$i.b", "hasItem[].$last.serialNumber")
      copy_field("$i.q", "hasItem[].$last.enumerationAndChronology")
      copy_field("$i.w","hasItem[].$last.currentLibrary")
      copy_field("$i.x","hasItem[].$last.currentLocation")
      call_macro("holdingLibrary+opacLink", mmsIdField:"$i.a",libraryCodeField:"$i.w")
    end
  end
end

add_array("@HOL-M_POR-M") # Helper element for creating Holding from MBD ("NurTitel")
do list(path: "HOL  ", "var": "$i")
  call_macro("deSol1BridgeTester", holdingId: "$i.8")
  unless exists("$i.deSol1Bridge") # exclude DE-Sol1-Bridge items/holdings because we add them otherwise
    copy_field("$i.M","@HOL-M_POR-M.$append")
    unless in("$i.8", "@ITM-H") # Checks if there is no corresponding ITM-Field
      do list(path:"H52??", "var": "$H52")
        if in("$i.8", "$H52.8")
          if exists("$H52.b")
            call_macro("suppressedLocation", targetField: "$H52", libraryCodeSubfield: "b", locationCodeSubfield: "c")
            unless exists("$H52.suppressedLocation") # Test if location is suppressed with mapping provided by the libraries.
              add_hash( "hasItem[].$append")
              add_field("hasItem[].$last.label", "lobid Bestandsressource")
              add_array("hasItem[].$last.type[]", "Item","PhysikalischerTitel")
              if exists("$H52.b.1")
                copy_field("$H52.b.1","hasItem[].$last.currentLibrary")
              else
                copy_field("$H52.b","hasItem[].$last.currentLibrary")
              end
              copy_field("$H52.c","hasItem[].$last.currentLocation")
              copy_field("$H52.h", "hasItem[].$last.callNumber")
              if exists("$H52.b.1")
                call_macro("holdingLibrary+opacLink", mmsIdField:"$i.8",libraryCodeField:"$H52.b.1")
              else
                call_macro("holdingLibrary+opacLink", mmsIdField:"$i.8",libraryCodeField:"$H52.b")
              end
              do list(path:"H66??", "var": "$H66")
                if in("$i.8", "$H66.8")
                  copy_field("$H66.a","hasItem[].$last.enumerationAndChronology")
                end
              end
            end
          end
        end
      end
    end
  end
end

do list(path:"POR  ", "var": "$i")
  copy_field("$i.M","@HOL-M_POR-M.$append")
# entity for every POR  .a without POR  .A
  unless any_match("$i.a",".*6441$") # filter out hbz
    add_hash( "hasItem[].$append")
    add_array("hasItem[].$last.type[]", "Item", "DigitalDocument")
    add_field("hasItem[].$last.label", "Electronic Portfolio")
    copy_field("$i.n", "hasItem[].$last.enumerationAndChronology")
    copy_field("$i.D", "$i.@electronicLocator")
    replace_all("$i.@electronicLocator","https://eu04.alma.exlibrisgroup.com/view/uresolver/49HBZ_NETWORK","")
    paste("hasItem[].$last.electronicLocator", "~https://eu04.alma.exlibrisgroup.com/view/uresolver/","$i.M","$i.@electronicLocator", join_char: "")
    copy_field("$i.d", "$i.@sublocation")
    replace_all("$i.@sublocation","https://hbz-network.userservices.exlibrisgroup.com/view/uresolver/49HBZ_NETWORK","")
    paste("hasItem[].$last.sublocation", "~https://hbz-network.userservices.exlibrisgroup.com/view/uresolver/","$i.M","$i.@sublocation", join_char: "")
    call_macro("holdingLibrary+opacLink", mmsIdField:"$i.a",libraryCodeField:"$i.g")
  end
  # entity for every POR  .A
  if exists ("$i.A")
    copy_field("$i.D", "$i.@electronicLocator")
    replace_all("$i.@electronicLocator","https://eu04.alma.exlibrisgroup.com/view/uresolver/49HBZ_NETWORK","")
    copy_field("$i.d", "$i.@sublocation")
    replace_all("$i.@sublocation","https://hbz-network.userservices.exlibrisgroup.com/view/uresolver/49HBZ_NETWORK","")
    do list(path:"$i.A", "var": "$j")
      unless any_equal("$j", "49HBZ_HGB")
        add_hash( "hasItem[].$append")
        add_array("hasItem[].$last.type[]", "Item", "DigitalDocument")
        add_field("hasItem[].$last.label", "Electronic Portfolio")
        paste("hasItem[].$last.electronicLocator", "~https://eu04.alma.exlibrisgroup.com/view/uresolver/","$j","$i.@electronicLocator", join_char: "")
        paste("hasItem[].$last.sublocation", "~https://hbz-network.userservices.exlibrisgroup.com/view/uresolver/","$j","$i.@sublocation", join_char: "")
        copy_field("$i.n", "hasItem[].$last.enumerationAndChronology")
        copy_field("$j", "$i.@iz")
        lookup("$i.@iz", "alma-iz-code-to-isil")
        call_macro("opacLink", field:"@iz")
        copy_field("$i.@iz","hasItem[].$last.heldBy.isil")
        paste("hasItem[].$last.heldBy.id", "~http://lobid.org/organisations/", "hasItem[].$last.heldBy.isil", "~#!", join_char:"")
        copy_field("hasItem[].$last.heldBy.id", "hasItem[].$last.heldBy.label")
        add_array("hasItem[].$last.inCollection[]")
        paste("hasItem[].$last.inCollection[].$append.id", "~http://lobid.org/organisations/", "$i.@iz", "~#!", join_char:"")
        copy_field("hasItem[].$last.inCollection[].$last.id", "hasItem[].$last.inCollection[].$last.label")
        # item id is constructed "http://lobid.org/items/[almaMmsId of the record]:[isil of the Owner]:[almaMmsId of the holding]#!"
        paste("hasItem[].$last.id", "~http://lobid.org/items/","almaMmsId", "~:", "hasItem[].$last.heldBy.isil","~:", "$i.a","~#!", join_char: "")
      end
    end
  end
end

do list(path: "MBD  ", "var": "$i")
  call_macro("deSol1BridgeTester", holdingId: "$i.i")
  unless exists("$i.deSol1Bridge") # exclude DE-Sol1-Bridge items/holdings because we add them otherwise
    unless any_match("$i.M","49HBZ_NETWORK")
      unless in("$i.M", "@HOL-M_POR-M") # Checks if there is no corresponding HOL or POR-Field
        add_hash( "hasItem[].$append")
        add_field("hasItem[].$last.label", "lobid Bestandsressource")
        add_array("hasItem[].$last.type[]", "Item","NurTitel")
        copy_field("$i.i", "$i.@iz")
        replace_all("$i.@iz",".*(\\d{4})$","$1")
        lookup("$i.@iz", "alma-institution-code-to-isil")
        call_macro("opacLink", field:"@iz")
        copy_field("$i.@iz","hasItem[].$last.heldBy.isil")
        paste("hasItem[].$last.heldBy.id", "~http://lobid.org/organisations/", "hasItem[].$last.heldBy.isil", "~#!", join_char:"")
        copy_field("hasItem[].$last.heldBy.id", "hasItem[].$last.heldBy.label")
        add_array("hasItem[].$last.inCollection[]")
        paste("hasItem[].$last.inCollection[].$append.id", "~http://lobid.org/organisations/", "$i.@iz", "~#!", join_char:"")
        copy_field("hasItem[].$last.inCollection[].$last.id", "hasItem[].$last.inCollection[].$last.label")
      # item id is constructed "http://lobid.org/items/[almaMmsId of the record]:[isil of the Owner]:[almaMmsId of the holding]#!"
        paste("hasItem[].$last.id", "~http://lobid.org/items/","almaMmsId", "~:", "hasItem[].$last.heldBy.isil","~:", "$i.i", "~#!", join_char: "")
      end
    end
  end
end

# ----provenanceInformation---

do list(path: "361??", "var":"$i")
  paste("$i.@ownerAndCallnumber","$i.5","$i.s")
  unless in("$i.@ownerAndCallnumber","hasItem[].*.@ownerAndCallnumber")
    copy_field("$i.@ownerAndCallnumber","hasItem[].$append.@ownerAndCallnumber")
    add_array("hasItem[].$last.type[]", "Item")
    paste("hasItem[].$last.heldBy.id", "~http://lobid.org/organisations/", "$i.5", "~#!", join_char:"")
    copy_field("hasItem[].$last.heldBy.id", "hasItem[].$last.heldBy.label")
    copy_field("$i.s", "hasItem[].$last.callNumber")
  end
  do list(path:"hasItem[]","var":"$x")
    if in("$i.@ownerAndCallnumber","$x.@ownerAndCallnumber")
      add_array("$x.custodialHistory[].$append.type[]","Event")
      copy_field("$i.a","$x.custodialHistory[].$last.constribution[].$append.agent.label")
      do list(path:"$i.0", "var":"$j")
        if any_match("$j","^\\(DE-588\\)(.*)$")
          copy_field("$j","$x.custodialHistory[].$last.constribution[].$last.agent.id")
          replace_all("$x.custodialHistory[].$last.constribution[].$last.agent.id", "^\\(DE-588\\)(.*)$", "https://d-nb.info/gnd/$1")
        end
      end
      copy_field("$i.o","$x.custodialHistory[].$last.typeOfOwnership")
      copy_field("$i.f","$x.custodialHistory[].$last.proof")
      copy_field("$i.z","$x.custodialHistory[].$last.note[].$append")
      copy_field("$i.k","$x.custodialHistory[].$last.startDate")
      replace_all("$x.custodialHistory[].$last.startDate","^(\\d{4})(\\d{2})(\\d{2})$","$1-$2-$3")
      copy_field("$i.l","$x.custodialHistory[].$last.dateStatement")
      copy_field("$i.u","$x.custodialHistory[].$last.seeAlso[].$append")
    end
  end
end

do list(path:"hasItem[]","var":"$i")
  remove_field("$i.@ownerAndCallnumber")
end

# 3611$5 - 3611$s - 3611$o - 3611$a - 3611$0 - 3611$f - 3611$z - 3611$l


lookup("hasItem[].*.heldBy.label", "lobidOrgLabels",delete:"true")
lookup("hasItem[].*.inCollection[].*.label", "lobidOrgLabels",delete:"true")

do list(path: "hasItem[]","var":"$i")
  if any_match("$i.inCollection[].*.id","http:\\/\\/lobid.org\\/organisations\\/7816..")
    add_field("$i.inCollection[].1.id","http://lobid.org/organisations/BRIDGE_FIZ-7816#!")
    add_field("$i.inCollection[].1.label","Bibliotheken ohne zentrale Katalogisierung (IZ Bridge)")
  end
end

copy_field("id", "@DE-Sol1-Holdings")
lookup("@DE-Sol1-Holdings","sol1Holdings",delete:"true")
replace_all("@DE-Sol1-Holdings",'""','"')
from_json("@DE-Sol1-Holdings")
do list(path:"@DE-Sol1-Holdings","var":"$i")
  copy_field("$i","hasItem[].$append")
end


