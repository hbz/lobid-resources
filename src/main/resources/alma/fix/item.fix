set_array("hasItem[]")

do list(path:"ITM  ", "var": "$i")
  add_field( "hasItem[].$append.test","")
  add_field("hasItem[].$last.label", "lobid Bestandsressource")
  set_array("hasItem[].$last.type[]", "Item","PhysicalObject")
  copy_field("$i.c", "hasItem[].$last.callNumber")
  unless exists("hasItem[].$last.callNumber")
    copy_field("$i.n", "hasItem[].$last.callNumber")
  end
  copy_field("$i.b", "hasItem[].$last.serialNumber")
  paste("hasItem[].$last.currentLocation", "$i.w", "~/", "$i.x")
  copy_field("$i.a", "hasItem[].$last.heldBy.id")
  replace_all("hasItem[].$last.heldBy.id",".*(\\d{4})$","$1")
  lookup("hasItem[].$last.heldBy.id", "alma-institution-code-to-isil")
  copy_field("hasItem[].$last.heldBy.id","hasItem[].$last.heldBy.isil")
  prepend("hasItem[].$last.heldBy.id", "http://lobid.org/organisations/")
  append("hasItem[].$last.heldBy.id","#!")
  paste("hasItem[].$last.id", "~http://lobid.org/items/","almaMmsId", "~:", "hasItem[].$last.heldBy.isil","~:", "$i.w","~:", "hasItem[].$last.callNumber","~#!", join_char: "")
end

do list(path: "HOL  ", "var": "$i")
  unless in("$i.8", "ITM  .H")
    unless in("$i.8", "ITM  .*.H")
      add_field( "hasItem[].$append.test","")
      add_field("hasItem[].$last.label", "lobid Bestandsressource")
      set_array("hasItem[].$last.type[]", "Item","PhysikalischerTitel")
      copy_field("$i.8", "hasItem[].$last.heldBy.id")
      replace_all("hasItem[].$last.heldBy.id",".*(\\d{4})$","$1")
      lookup("hasItem[].$last.heldBy.id", "alma-institution-code-to-isil")
      copy_field("hasItem[].$last.heldBy.id","hasItem[].$last.heldBy.isil")
      prepend("hasItem[].$last.heldBy.id", "http://lobid.org/organisations/")
      append("hasItem[].$last.heldBy.id","#!")
      paste("hasItem[].$last.id", "~http://lobid.org/items/","almaMmsId", "~:", "hasItem[].$last.heldBy.isil","~#!", join_char: "")
      do list(path:"H52??", "var": "$H52")
        if in("$i.8", "$H52.8")
          paste("hasItem[].$last.currentLocation", "$H52.b", "~/", "$H52.c")
          copy_field("$H52.h", "hasItem[].$last.callNumber")
          paste("hasItem[].$last.id", "~http://lobid.org/items/","almaMmsId", "~:", "hasItem[].$last.heldBy.isil","~:","$H52.b","~#!", join_char: "")
        end
      end
    end
  end
end

do list(path: "MBD  ", "var": "$i")
  unless any_match("$i.M","49HBZ_NETWORK")
    unless in("$i.M", "HOL  .M")
      unless in("$i.M", "HOL  .*.M")
        unless in("$i.M", "POR  .M")
          unless in("$i.M", "POR  .*.M")
            add_field( "hasItem[].$append.test","")
            add_field("hasItem[].$last.label", "lobid Bestandsressource")
            set_array("hasItem[].$last.type[]", "Item","NurTitel")
            copy_field("$i.i", "hasItem[].$last.heldBy.id")
            replace_all("hasItem[].$last.heldBy.id",".*(\\d{4})$","$1")
            lookup("hasItem[].$last.heldBy.id", "alma-institution-code-to-isil")
            copy_field("hasItem[].$last.heldBy.id","hasItem[].$last.heldBy.isil")
            prepend("hasItem[].$last.heldBy.id", "http://lobid.org/organisations/")
            append("hasItem[].$last.heldBy.id","#!")
            paste("hasItem[].$last.id", "~http://lobid.org/items/","almaMmsId", "~:", "hasItem[].$last.heldBy.isil","~#!", join_char: "")
          end
        end
      end
    end
  end
end

do list(path:"POR  ", "var": "$i")
# entity for every POR  .a without POR  .A
  unless any_match("$i.a",".*6441$") # filter out hbz
    add_field( "hasItem[].$append.test","")
    set_array("hasItem[].$last.type[]", "Item", "DigitalDocument")
    add_field("hasItem[].$last.label", "Electronic Portfolio")
    copy_field("$i.D", "$i.@electronicLocator")
    replace_all("$i.@electronicLocator","https://eu04.alma.exlibrisgroup.com/view/uresolver/49HBZ_NETWORK","")
    paste("hasItem[].$last.electronicLocator", "~https://eu04.alma.exlibrisgroup.com/view/uresolver/","$i.M","$i.@electronicLocator", join_char: "")
    copy_field("$i.d", "$i.@sublocation")
    replace_all("$i.@sublocation","https://hbz-network.userservices.exlibrisgroup.com/view/uresolver/49HBZ_NETWORK","")
    paste("hasItem[].$last.sublocation", "~https://hbz-network.userservices.exlibrisgroup.com/view/uresolver/","$i.M","$i.@sublocation", join_char: "")
    copy_field("$i.a", "hasItem[].$last.heldBy.id")
    replace_all("hasItem[].$last.heldBy.id",".*(\\d{4})$","$1")
    lookup("hasItem[].$last.heldBy.id", "alma-institution-code-to-isil")
    copy_field("hasItem[].$last.heldBy.id","hasItem[].$last.heldBy.isil")
    prepend("hasItem[].$last.heldBy.id", "http://lobid.org/organisations/")
    append("hasItem[].$last.heldBy.id","#!")
    paste("hasItem[].$last.id", "~http://lobid.org/items/","almaMmsId", "~:", "hasItem[].$last.heldBy.isil","~:", "$i.a","~#!", join_char: "")
  end
  # entity for every POR  .A
  if exists ("$i.A")
    copy_field("$i.D", "$i.@electronicLocator")
    replace_all("$i.@electronicLocator","https://eu04.alma.exlibrisgroup.com/view/uresolver/49HBZ_NETWORK","")
    copy_field("$i.d", "$i.@sublocation")
    replace_all("$i.@sublocation","https://hbz-network.userservices.exlibrisgroup.com/view/uresolver/49HBZ_NETWORK","")
    do list(path:"$i.A", "var": "$j")
      add_field( "hasItem[].$append.test","")
      set_array("hasItem[].$last.type[]", "Item", "DigitalDocument")
      add_field("hasItem[].$last.label", "Electronic Portfolio")
      paste("hasItem[].$last.electronicLocator", "~https://eu04.alma.exlibrisgroup.com/view/uresolver/","$j","$i.@electronicLocator", join_char: "")
      paste("hasItem[].$last.sublocation", "~https://hbz-network.userservices.exlibrisgroup.com/view/uresolver/","$j","$i.@sublocation", join_char: "")
      copy_field("$j", "hasItem[].$last.heldBy.id")
      lookup("hasItem[].$last.heldBy.id", "alma-iz-code-to-isil")
      copy_field("hasItem[].$last.heldBy.id","hasItem[].$last.heldBy.isil")
      prepend("hasItem[].$last.heldBy.id", "http://lobid.org/organisations/")
      append("hasItem[].$last.heldBy.id","#!")
      paste("hasItem[].$last.id", "~http://lobid.org/items/","almaMmsId", "~:", "hasItem[].$last.heldBy.isil","~:", "$i.a","~#!", join_char: "")
    end
  end
end
