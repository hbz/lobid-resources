# 008 - Fixed-Length Data Elements-General Information (NR) - No subfields

set_array("@language")
copy_field("008", "@008-lang")
substring("@008-lang", "35", "38")
copy_field("008-lang", "@language.$append")
copy_field("041[ 01] .[adj]", "@language.$append")

uniq("@language")

set_array("language[]")
do list(path:"@language", "var":"$i")
  copy_field("$i", "language[].$append.id")
  copy_field("$i", "language[].$last.label")
end

lookup("language[].*.label","ISO639-2-to-GND")
prepend("language[].*.id", "http://id.loc.gov/vocabulary/iso639-2/")


# 300 - Physical Description (R)
# Old Morph MAB-> Extent was less complex: 		<data source="433[-abc][-1].[aq]|653-1.a" name="http://id.loc.gov/ontologies/bibframe/extent"/>
# TODO: Figure out if we need the extent info as complex as the introx info. I reuse the introx transformation here.

set_array("@300a")
copy_field("300  .a", "@300a")
if exists("@300a.1")
  copy_field("@300a.1", "@300a1")
  replace_all("@300a1", "\\s?[:;+(]?$", "")
end
if exists("@300a.2")
  copy_field("@300a.2", "@300a2")
  replace_all("@300a2", "\\s?[:;+)]?$", "")
end
do list(path: "300  .b", "var": "$i")
  replace_all("$i", "\\s?[:;+(]?$", "")
  copy_field("$i", "@300b")
end
set_array("@300c")
copy_field("300  .c", "@300c")
if exists("@300c.1")
  copy_field("@300c.1", "@300c1")
  replace_all("@300c1", "[.]?\\s?[:;+(]?$", "")
end
if exists("@300c.2")
  copy_field("@300c.2", "@300c2")
  replace_all("@300c2", "[.]?\\s?[:;+)]?$", "")
end
do list(path: "300  .e", "var": "$i")
  replace_all("$i", "[.]?\\s?\\(?$", "")
  copy_field("$i", "@300e")
end
if exists("@300a2")
  if exists("@300c2")
    paste("@300a2_punct", "@300a2", "~ ; ", join_char: "")
  else
    paste("@300a2_punct", "@300a2", "~)", join_char: "")
  end
end
if exists("@300b")
  if exists("@300a1")
    paste("@300b_punct", "~ : ", "@300b", join_char: "")
  else
    copy_field("@300b", "@300b_punct")
  end
end
if exists("@300c1")
  if exists("@300a1|@300b")
    paste("@300c1_punct", "~ ; ", "@300c1", join_char: "")
  else
    copy_field("@300c1", "@300c1_punct")
  end
end
copy_field("@300c2", "@300c2_punct")
if exists("300  .e")
  if exists("@300[ac]1|@300b")
    if exists("@300[ac]2")
      paste("@300e_punct", "~ ; ", "@300e", "~ (", join_char: "")
    else
      paste("@300e_punct", "~ ; ", "@300e", join_char: "")
    end
  else
    copy_field("@300e", "@300e_punct")
  end
end
paste("extent", "@300a1", "@300b_punct", "@300c1_punct", "@300e_punct", "@300a2_punct", "@300c2_punct", join_char: "")


# 500 - General Note (R) Subfield: $a (NR)
set_array("note[]")
do list(path:"500  ", "var": "$i")
  copy_field("$i.a", "note[].$append")
end
uniq("note[]")


# 520 - Summary, Etc. (R) Subfield: $a (NR), $b (NR)
# TODO: Check if 1. Indicator 1 (REVIEW) is really to count as abstract. Perhaps 2 (Scope and content) is a better fit.
set_array("abstract[]")
do list(path:"520[ 13] ", "var": "$i")
  copy_field("$i.[ab]", "abstract[].$append")
end

# 502 - Dissertation Note (R) Subfield: $a (R)
set_array("thesisInformation[]")
do list(path:"502  ", "var": "$i")
  copy_field("$i.a", "thesisInformation[].$append")
end


# bibliographicCitation
# TODO: This element seems to be not very informative due to the distributed and uncomplete information.
# Also the element bibliographicCitation is confusing since it has a broader concept than the scope in lobid: only bibliographicInfo about issues/collection that an article belongs to.
# Do we need a filter only for articles?

set_array("bibliographicCitation")


copy_field("77308.t","bibliographicCitation.$append")
copy_field("77308.b","bibliographicCitation.$append")
copy_field("77308.d","bibliographicCitation.$append")
copy_field("77308.g","bibliographicCitation.$append")
copy_field("77308.n","bibliographicCitation.$append")
copy_field("77308.n","bibliographicCitation.$append")
copy_field("501??.a","bibliographicCitation.$append")

replace_all("bibliographicCitation.*","<<|>>","")
replace_all("bibliographicCitation.*","^http","Siehe: http")

join_field("bibliographicCitation", "; ")
