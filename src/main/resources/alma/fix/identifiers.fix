copy_field("001","almaMmsId")

paste("id", "~http://lobid.org/resources/", "001", "~#!", join_char: "")


# 024 - Other Standard Identifier (R) Subfield: $a (NR) $2 (NR)
# urn

set_array("urn[]")

do list(path: "0247?", "var": "$i")
  if any_equal("$i.2","urn")
      copy_field("$i.a","urn[].$append")
  end
end

replace_all("urn[].*","^(nbn:de:.*\\d)$","urn:$1")

# 035 - System Control Number (R) - Subfield: $a (NR)

do list(path: "035  ", "var":"$i")
  if any_match("$i.a", "\\(DE-605\\)\\D\\D(.*)")
    copy_field("$i.a", "hbzId")
  end
end

replace_all("hbzId","\\(DE-605\\)(.*)","$1")

# 020 - International Standard Book Number (R) - $a (NR)
# source data sometimes provides repeated subfield $a even if this is not valid marc

set_array("@isbn[]")
set_array("isbn[]")

do list(path:"020  ", "var": "$i")
  do list(path:"$i.a", "var": "$j")
    copy_field("$j","@isbn[].$append")
  end
end

do list(path:"@isbn[]", "var": "$i")
  isbn("$i", to: "clean")
  copy_field("$i", "isbn[].$append")
  copy_field("$i", "isbn[].$append")
  if any_match("$i", ".{13}")
    isbn("isbn[].$last", to:"isbn10")
  elsif any_match("$i", ".{10}")
    isbn("isbn[].$last", to:"isbn13")
  end
end

uniq("isbn[]")


# 022 - International Standard Serial Number (R) - Subfield $a (NR)
set_array("issn[]")
do list(path:"022? ", "var":"$i")
  copy_field("$i.a", "issn[].$append")
end
replace_all("issn[].*", "-","")


# 024 - Other Standard Identifier (R) - Subfield a (NR) 1. Indicator 2 = ISMN
set_array("ismn[]")
do list(path:"0242?", "var":"$i")
  copy_field("$i.a", "ismn[].$append")
end
replace_all("ismn[].*", "-","")

# TODO: Specify if DOI should be URI?
# 024 (R) Subfield a (NR) 1. Indicator 7 = to defined Identifier
set_array("doi[]")
do list(path:"0247?", "var":"$i")
  if all_equal("$i.2","doi")
    copy_field("$i.a", "doi[].$append")
  end
end

# 035 - System Control Number (R) - Subfield: $a (NR)
set_array("oclcNumber[]")

do list(path:"035  ", "var":"$i")
    if all_match("$i.a", "\\(OCoLC\\)(.*)")
      copy_field("$i.a", "oclcNumber[].$append")
    end
end
replace_all("oclcNumber[].*", "\\(OCoLC\\)","")


# 035 - System Control Number (R) - Subfield: $a (NR)
do list(path:"035  ", "var":"$i")
  if all_match("$i.a", "\\(DE-600\\)(.*)")
    copy_field("$i.a", "zdbId")
  end
end
replace_all("zdbId", "\\(DE-600\\)","")

