copy_field("001","almaMmsId")

paste("id", "~http://lobid.org/resources/", "001", "~#!", join_char: "")


# 024 - Other Standard Identifier (R) Subfield: $a (NR) $2 (NR)
# urn

set_array("urn[]")

do list(path: "0247?", "var": "$i")
  if any_equal("$i.2","urn")
      copy_field("$i.a","urn[].$append")
  end
end

# Sometimes urn are not set in 024 then we could pick up the missing from 856.
# 856 - Electronic Location and Access (R) - Subfield: $u (R) $3 (NR)
# 1. Indicator: 4 = HTTP
set_array("@urnLinks")

do list(path:"856??", "var":"$i")
  if all_match("$i.u", "^http.*(urn=|\\.(org|de)/)urn:.+$") # This should ignore repository links like: https://sammlungen.ulb.uni-muenster.de/urn/urn:nbn:de:hbz:6-85659520092
    copy_field("$i.u", "urn[].$append")
    copy_field("$i.u", "@urnLinks.$append")
    replace_all("urn[].$last", "^http.*[/=](urn:.+$)", "$1")
  end
end

replace_all("urn[].*","^(nbn:de:.*\\d)$","urn:$1")
uniq("@urnLinks")
uniq("urn[]")

# 035 - System Control Number (R) - Subfield: $a (NR)

do list(path: "035  ", "var":"$i")
  if any_match("$i.a", "\\(DE-605\\)\\D\\D(.*)")
    copy_field("$i.a", "hbzId")
  end
end

replace_all("hbzId","\\(DE-605\\)(.*)","$1")

# add a deprecatedUri to all records with hbzId to document all old lobid urls.
if exists("hbzId")
  paste("deprecatedUri", "~http://lobid.org/resources/", "hbzId", "~#!", join_char: "")
end


# 020 - International Standard Book Number (R) - $a (NR)
# source data sometimes provides repeated subfield $a even if this is not valid marc

set_array("@isbn[]")
set_array("isbn[]")

do list(path:"020  ", "var": "$i")
  do list(path:"$i.a", "var": "$j")
    copy_field("$j","@isbn[].$append")
  end
end

do list(path:"@isbn[]", "var": "$i")
  isbn("$i", to: "clean")
  copy_field("$i", "isbn[].$append")
  copy_field("$i", "isbn[].$append")
  if any_match("$i", ".{13}")
    isbn("isbn[].$last", to:"isbn10")
  elsif any_match("$i", ".{10}")
    isbn("isbn[].$last", to:"isbn13")
  end
end

uniq("isbn[]")


# 022 - International Standard Serial Number (R) - Subfield $a (NR)
set_array("issn[]")
do list(path:"022? ", "var":"$i")
  copy_field("$i.a", "issn[].$append")
end
replace_all("issn[].*", "-","")
uniq("issn[]")

# 024 - Other Standard Identifier (R) - Subfield a (NR) 1. Indicator 2 = ISMN
set_array("ismn[]")
do list(path:"0242?", "var":"$i")
  copy_field("$i.a", "ismn[].$append")
end
replace_all("ismn[].*", "-","")


# 024 (R) Subfield a (NR) 1. Indicator 7 = to defined Identifier
set_array("doi[]")
do list(path:"0247?", "var":"$i")
  if all_equal("$i.2","doi")
    copy_field("$i.a", "doi[].$append")
  end
end

# Sometimes dois are not set in 024 then we could pick up the missing from 856.
# 856 - Electronic Location and Access (R) - Subfield: $u (R) $3 (NR)
# 1. Indicator: 4 = HTTP
do list(path:"856??", "var":"$i")
  if all_match("$i.u", ".*doi.org.*(10\\.(\\d)+/(\\S)+).*") # Volltext
    copy_field("$i.u", "doi[].$append")
  end
end
replace_all("doi[].*", ".*doi.org.*(10\\.(\\d)+/(\\S)+).*", "$1")
uniq("doi[]")

# 035 - System Control Number (R) - Subfield: $a (NR)
set_array("oclcNumber[]")

do list(path:"035  ", "var":"$i")
    if all_match("$i.a", "\\(OCoLC\\)(.*)")
      copy_field("$i.a", "oclcNumber[].$append")
    end
end
replace_all("oclcNumber[].*", "\\(OCoLC\\)","")

#160 - 016 - National Bibliographic Agency Control Number (R)
do list(path:"0167 ", "var":"$i")
  unless exists("zdbId")
    if any_match("$i.2","DE-600")
      copy_field("$i.a","zdbId")
    end
  end

# dnbId
  if any_match("$i.2","DE-101")
    copy_field("$i.a","dnbId")
  end
end

# 035 - System Control Number (R) - Subfield: $a (NR)
do list(path:"035  ", "var":"$i")
  unless exists("zdbId")
    if all_match("$i.a", "\\(DE-600\\)(.*)")
      copy_field("$i.a", "zdbId")
    elsif all_match("$i.a", "\\(DE-599\\)(ZDB.*)")
      copy_field("$i.a", "zdbId")
    end
  end
end

# clean up ZDB
replace_all("zdbId", "\\(DE-600\\)","")
replace_all("zdbId", "\\(DE-599\\)ZDB","")
replace_all("zdbId", "(\\d{1,7})-* ?-*([Xx\\d])","$1-$2") # CZ entries have incorrect whitespaces sometimes in the zdbId, we need to adjust them so only one "-" separates the first group of numbers from the last number.


copy_field("almaMmsId","rpbId")
lookup("rpbId","almaMmsId2rpbId",delete:"true")
replace_all("rpbId", "^RPB","")

set_array("stockNumber[]")
do list(path:"028??", "var":"$i")
  copy_field("$i.a", "stockNumber[].$append")
end

unless exists("hbzId")
  if exists("@inNZ")
    copy_field("zdbId","@hbzId")
    lookup("@hbzId","zdbId2oldHbzId",delete:"true")
    if exists("@hbzId")
      paste("deprecatedUri", "~http://lobid.org/resources/", "@hbzId", "~#!", join_char: "")
    end
    copy_field("@hbzId","hbzId")
  end
end
